MODULE UploadDocZip;

REQUIRE EdmDocument, EdmDebtDashboard, EdmDocDashboard;

CLASS EnumUploadZip 'Выгрузка в zip' {
    all '1. Все отмеченные', noEdm '2. Все отмеченные без ЭДО'
}

FORM enumUploadZip 'Выгрузка документов в zip'
    OBJECTS o = EnumUploadZip
    PROPERTIES (o) READONLY staticCaption
    ORDERS staticCaption(o)
;

DESIGN enumUploadZip {PROPERTY (staticCaption(o)) {caption = 'Выбор операции';} }


fnameDoc 'Имя документа' (Document d) =
    IF nameDocType(d) = 'Договор' THEN nameLegalEntity(d) + '_Договор ' + number(contract(d)) + ' от ' + toDateDDMMYY(date(d)) + '.doc' ELSE
    IF nameDocType(d) = 'Соглашение' THEN nameLegalEntity(d) + '_Договор ' + number(contract(d)) + ' Соглашение ' + number(d) + ' от ' + toDateDDMMYY(date(d)) + '.doc' ELSE
    IF nameDocType(d) = 'Акт выполненных работ' THEN nameLegalEntity(d) + '_Договор ' + number(contract(d)) + ' Акт ' + number(d) + ' от ' + toDateDDMMYY(date(d)) + '.doc' 
    ELSE number(d) + ' от ' + toDateDDMMYY(date(d)) + '.doc';    

createZipDoc 'Выгрузить в zip' () {
    IF NOT GROUP SUM 1 IF in(Document d) THEN { MESSAGE 'Нет отмеченных записей'; RETURN; } 
    DIALOG enumUploadZip OBJECTS o INPUT DO {
        zipping(STRING[100] f) <- NULL;
        IF o = EnumUploadZip.all THEN {
            FOR (Document d IS Document) AND in(d) DO { zipping(STRING[100] f) <- file(d) WHERE f == fnameDoc(d); }
        } ELSE {
            FOR (Document d IS Document) AND in(d)  AND NOT edm(d) DO { zipping(STRING[100] f) <- file(d) WHERE f == fnameDoc(d); }
        }
        makeZipFile();
        open(zipped(),'archive');
    }
}

createZipAct 'Выгрузить в zip' () {
    IF NOT GROUP SUM 1 IF in(Debt d) THEN { MESSAGE 'Нет отмеченных записей'; RETURN; }
    DIALOG enumUploadZip OBJECTS o INPUT DO {
        zipping(STRING[100] f) <- NULL;
        IF o = EnumUploadZip.all THEN {
            FOR (Debt d IS Debt) AND in(d) DO { zipping(STRING[100] f) <- file(act(d)) WHERE f == fnameDoc(act(d)); }
        } ELSE {
            FOR (Debt d IS Debt) AND in(d)  AND NOT actEdm(d) DO { zipping(STRING[100] f) <- file(act(d)) WHERE f == fnameDoc(act(d)); }
        }
        makeZipFile();
        open(zipped(),'archive');
    }
}


EXTEND FORM docDashboard PROPERTIES() DRAW d TOOLBAR createZipDoc;
EXTEND FORM debtDashboard PROPERTIES() DRAW d TOOLBAR createZipAct;
