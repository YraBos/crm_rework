MODULE DebtDashboardNew;

REQUIRE DebtDashboard, Utils, Time;

filterSumDebt 'Значение задолженности' = DATA LOCAL INTEGER ();

sumContract 'Сумма договора' (Debt d) = round(sum(contract(d)),2);
sumAgreement 'Сумма соглашения' (Debt d)  = round(sum(agreement(d)),2);
dateContract 'Дата договора' (Debt d) = date(contract(d));
dateAgreement 'Дата соглашения' (Debt d) = date(agreement(d));
balance 'Остаток' (Debt d) = round(sum(d) - IF paid(d) THEN paid(d) ELSE 0.00, 2);
numberAct 'Номер акта' (Debt d) = number(act(d));
dateAct 'Дата акта' (Debt d) = date(act(d));
nameAct 'Название акта' (Debt d) = name(act(d));
sumAct 'Сумма акта' (Debt d) = sum(act(d));

dateContractISO '' (Debt d) = toDateISO(dateContract(d));
agrFlag '' (Debt d) = IF agreement(d) THEN 1 ELSE 0;
actFlag '' (Debt d) = IF act(d) THEN 1 ELSE 0;

// связано с валютными договорами
inCurrency 'USD' (Debt d) = IF inCurrency(contract(d)) THEN 1 ELSE 0;
sumContractCurrency 'Сумма договора USD' (Debt d) = round(sumCurrency(contract(d)),2);
sumAgreementCurrency 'Сумма соглашения USD' (Debt d) = round(sumCurrency(agreement(d)),2);

// расчет оплат(payment) USD относительно соглашений(agreement) и договоров(contract) 
paidPaymentCurrency 'Оплачено USD' (Agreement a) = GROUP SUM sumCurrency(Payment p) BY agreement(p) MATERIALIZED;
paidPaymentCurrency 'Оплачено' (Agreement a, DATE d) = GROUP SUM sumCurrency(Payment p) IF date(p) <= d BY agreement(p);

paidNotPaymentAgreementCurrency (Contract c) = paidCurrency(c) (-) [GROUP SUM paidPaymentCurrency(Agreement a) BY contract(a)](c);
paidContractCurrency 'Оплачено USD' (Agreement a) = PARTITION UNGROUP paidNotPaymentAgreementCurrency
                                                      LIMIT STRICT sumCurrency(a) (-) paidPaymentCurrency(a)
                                                      ORDER date(a),a 
                                                      BY contract(a) MATERIALIZED; 

paidCurrency 'Оплачено USD' (Agreement a) = paidContractCurrency(a) (+) paidPaymentCurrency(a);

// связываем расчеты оплат с задолженностями (debt)
paidAgreementCurrency 'Оплачено USD по соглашениям' (Debt d) = PARTITION UNGROUP paidPaymentCurrency
                                                      LIMIT STRICT sumCurrency(d)
                                                      ORDER date(d),d 
                                                      BY agreement(d) MATERIALIZED; 

paidNotDebtCurrency (Contract c) = paid(c) (-) [GROUP SUM paidAgreementCurrency(Debt d) BY contract(d)](c);

paidContractCurrency 'Оплачено USD по контракту без соглашений' (Debt d) = PARTITION UNGROUP paidNotDebtCurrency
                                                      LIMIT STRICT (sumCurrency(d) (-) paidAgreementCurrency(d)) IF NOT agreement(d)
                                                      ORDER date(d),d 
                                                      BY contract(d) MATERIALIZED; 

paidCurrency 'Оплачено USD' (Debt d) = paidAgreementCurrency(d) (+) paidContractCurrency(d);

paidAgreementCurrency 'Оплачено' (Debt d, DATE dt) = PARTITION UNGROUP paidPaymentCurrency
                                                                   LIMIT STRICT sumCurrency(d)
                                                                   ORDER date(d),d 
                                                                   BY agreement(d), dt;

paidCurrency 'Оплачено USD' (Contract c, DATE dt) = GROUP SUM sum(Payment p) IF date(p) <= dt BY contract(p);
paidNotDebtCurrency (Contract c, DATE dt) = paidCurrency(c, dt) (-) [GROUP SUM paidAgreementCurrency(Debt d, dt) BY contract(d)](c);

paidContractCurrency 'Оплачено USD' (Debt d, DATE dt) = PARTITION UNGROUP paidNotDebtCurrency
                                                      LIMIT STRICT (sumCurrency(d) (-) paidAgreementCurrency(d, dt)) IF NOT agreement(d)
                                                      ORDER date(d),d 
                                                      BY contract(d), dt; 

paidCurrency 'Оплачено USD' (Debt d, DATE dt) = paidAgreementCurrency(d, dt) (+) paidContractCurrency(d, dt);

balanceCurrency 'Остаток USD' (Debt d) = round(sumCurrency(d) - IF paidCurrency(d) THEN paidCurrency(d) ELSE 0.00, 2);



//legalEntity 'CID' (Debt d) = legalEntity(contract(d));
//name2 'О2' (Debt d) = name(legalEntity(d));
//sumEDebt 'ЗО' (LegalEntity le, DATE d) = GROUP SUM sum(Debt dd) (-) paid(dd, d) IF date(dd) <= d BY legalEntity(dd);

FORM reportDebs 'Печать'
    OBJECTS dd = DATE PANEL
    PROPERTIES VALUE (dd)
    OBJECTS c  = Customer PROPERTIES(c) name PANEL
    OBJECTS t  = ContractType PROPERTIES(t) name PANEL
    OBJECTS db = Debt
    PROPERTIES(db) date, nameLegalEntity, 
                   sum, paid, balance,                              // рублевый
                   sumCurrency, paidCurrency, balanceCurrency,      // валютный  
                   numberContract, dateContract, nameContract, sumContract, dateContractISO,
                   numberAgreement, dateAgreement, nameAgreement, sumAgreement,
                   numberAct, dateAct, nameAct, sumAct,
                   agrFlag, actFlag, inCurrency 
    PROPERTIES sumDebt(c,dd)
    FILTERS customer(db) == c, date(db) <= dd, type(contract(db)) == t
    FILTERS IF inCurrency(db) = 0 THEN abs(balance(db)) > 0 ELSE abs(balanceCurrency(db)) > 0
    FILTERS sumDebt(c, dd)
    FILTERS IF filterCustomerContractType() THEN t == filterCustomerContractType() ELSE TRUE 
    FILTERS IF filterSumDebt() THEN abs(sumDebt(c,t,dd)) > filterSumDebt() ELSE TRUE  
    ORDERS nameLegalEntity(db),dateContractISO(db),numberContract(db),date(db)
;

onReport 'Отчет' (Customer c, DATE dd) {
    PRINT reportDebs OBJECTS c = c, dd = dd XLS; // XLS
}

EXTEND FORM debtDashboard
    OBJECTS le = LegalEntity
    PROPERTIES filterSumDebt(), onReport(c, dd)
    PROPERTIES(db) paidCurrency,balanceCurrency
    EVENTS ON INIT {
        filterSumDebt() <- 100;
    }
    FILTERS customer(le) = c
    FILTERS IF filterSumDebt() THEN abs(sumDebt(c,t,dd)) > filterSumDebt() ELSE TRUE  
;

DESIGN debtDashboard {
    BOX (dd) {
        caption = 'Фильтры и отчеты';
        MOVE PROPERTY (filterSumDebt());
        MOVE PROPERTY (onReport(c, dd));
    }
}