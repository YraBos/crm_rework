MODULE FinCart;

REQUIRE Finance, Utils;

CLASS MSDFinance 'Учет финансов';

date 'Дата' = DATA DATE (MSDFinance);   // data
name 'Наименование' = DATA STRING[200] (MSDFinance); // naim
moveBYN 'Рубли' = DATA NUMERIC[10,2] (MSDFinance); // нет аналога
moveUSD 'Доллары' = DATA NUMERIC[10,2] (MSDFinance); // нет аналога
note 'Примечание' = DATA STRING[100] (MSDFinance); // prim
sign 'Признак' = DATA STRING[25] (MSDFinance); // prizn
document 'Документ' = DATA STRING[20] (MSDFinance); // ndok
code 'Код' = DATA STRING[4] (MSDFinance); // kod
operation 'КО' = DATA STRING[2] (MSDFinance); // kop
omes 'Месяц' = DATA STRING[8] (MSDFinance); // omes
source 'Источник данных' = DATA STRING[20]  (MSDFinance); // нет аналога
update 'Дата обновления информации' = DATA DATE (MSDFinance); // нет аналога

// prix, rasx, saldo - игнорируем, их заменяет moveBYN, moveUSD
// memo - игнорируем, по жизни пустое, иначе надо будет выводить, как табло

INDEX date(MSDFinance o);

saldoBYN 'Сальдо, BYN'  = GROUP SUM moveBYN(MSDFinance o) MATERIALIZED ;
saldoUSD 'Сальдо, USD'  = GROUP SUM moveUSD(MSDFinance o) MATERIALIZED ;

FORM viewFinance 'Учет финансов'
    OBJECTS o = MSDFinance LAST 
    PROPERTIES (o) READONLY date, document, name, moveBYN, moveUSD, note, sign, code, operation, omes, source, update
    OBJECTS b = MSDFinance PANEL 
    PROPERTIES (b) READONLY name, note 
    PROPERTIES () READONLY saldoBYN, saldoUSD
    FILTERS b=o 
    ORDERS date(o) 
;

onImportFin 'Импорт' (STRING[20] source) {
    INPUT dbfile = NAMEDFILE DO {;
        IF NOT (upper(name(dbfile)) = source AND upper(extension(dbfile)) = 'DBF') THEN {
            MESSAGE 'Не ожидаемое имя файла: ' + source + '.DBF\n' + 
                    '-------------------------------------\n' +
                    'Выбранный файл: ' + name(dbfile) + '.' + extension(dbfile);
            RETURN;
        }  
        NEWSESSION {         
            LOCAL data = DATE (INTEGER);
            LOCAL naim, ndok, prim, prizn, kod, kop, omes = STRING (INTEGER);
            LOCAL prix, rasx, saldo = NUMERIC[10,2] (INTEGER);
            LOCAL upd = DATE (); upd() <- currentDate();
            DELETE MSDFinance m WHERE m IS MSDFinance AND source(m) = source;
            IMPORT DBF CHARSET 'CP866' FROM dbfile TO data, naim, ndok, prim, prizn, kod, kop, omes, prix, rasx, saldo;
            FOR imported(INTEGER i) DO NEW o = MSDFinance {
                source(o) <- STRING[20](source);
                update(o) <- upd();
                date(o) <- data(i);
                name(o) <- STRING[200](naim(i));
                moveBYN(o) <- IF source = 'BALRUB' THEN (IF prix(i) != 0 THEN prix(i) ELSE -rasx(i)) ELSE NULL;
                moveUSD(o) <- IF source = 'BAL'    THEN (IF prix(i) != 0 THEN prix(i) ELSE -rasx(i)) ELSE NULL;
                document(o) <- STRING[20](ndok(i));
                note(o) <- STRING[100](prim(i));
                sign(o) <- STRING[25](prizn(i));
                code(o) <- STRING[4](kod(i));
                operation(o) <- STRING[2](kop(i));
                omes(o) <- STRING[8](omes(i));
            }
            APPLY ;
            
            SEEK viewFinance.o;
        }             
    }
}

onUpdateBalRub 'Импорт BYN' () {onImportFin('BALRUB');}
onUpdateBal 'Импорт USD' () {onImportFin('BAL');}

EXTEND FORM viewFinance
    PROPERTIES onUpdateBalRub() TOOLBAR , onUpdateBal() TOOLBAR 
;

DESIGN viewFinance {
    OBJECTS {
        NEW cntmain {
            horizontal = TRUE ;
            fill = 0.01;
            NEW cnt1 {
                fill = 1;
                caption = 'Сальдо';
                foreground = #000000;
                horizontal = FALSE ;
                MOVE PROPERTY (saldoBYN()) {background=#D7FFD7; caption = 'BYN';}
                MOVE PROPERTY (saldoUSD()) {background=#D7D7FF; caption = 'USD';}
            }
            NEW cnt2 {
                fill = 10;
                caption = 'Информация';
                foreground = #000000;
                horizontal = FALSE ;
                MOVE PROPERTY (name(b));
                MOVE PROPERTY (note(b));
            }
        }
        MOVE BOX(o);
    }   
}

NAVIGATOR {
   finance {
        NEW FOLDER dosfin 'Журналы MS-DOS' {
            NEW FORM viewFinance;  
        }
   } 
}