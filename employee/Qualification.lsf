MODULE Qualification;

REQUIRE Employee;

NAMESPACE Contact;

CLASS Qualification 'Квалификация';
TABLE qualification (Qualification);

name 'Квалификация'  = DATA STRING[100] (Qualification) NONULL;
mark 'Отм' = DATA LOCAL BOOLEAN (Qualification) CHARWIDTH 5;

lstQualification 'Квалификация' = DATA STRING (Employee);

FORM editQualification 'Редактирование'
    OBJECTS q = Qualification PANEL
    PROPERTIES (q) name 
    EDIT Qualification OBJECT q 
;

FORM viewQualification 'Справочник квалификаций'
    OBJECTS q = Qualification
    PROPERTIES (q) READONLY name
    PROPERTIES (q) NEWSESSION NEW, EDIT, DELETE 
    ORDERS name(q)
;

FORM listQualification 'Справочник квалификаций'
    OBJECTS q = Qualification
    PROPERTIES (q) mark
    PROPERTIES (q) READONLY name
    ORDERS name(q)
    LIST Qualification OBJECT q
;

NAVIGATOR {
    employeeFolder {
        NEW FORM viewQualification AFTER employees;
    }
}

onGetQualification 'Выбрать' (Employee o) {
    LOCAL cpart = STRING (); 
    LOCAL oq = Qualification();
    mark(Qualification o1) <- NULL; 
    IF lstQualification(o) THEN {
        FOR iterate(INTEGER i,1,wordCount(lstQualification(o),',')) DO {
            cpart() <- trim(getWord(lstQualification(o),',',i));
            oq() <- GROUP MAX (Qualification o2) IF name(o2) = cpart();
            mark(oq()) <- TRUE;
        }    
    }
    DIALOG listQualification;
    lstQualification(o) <- NULL; // поле в сотруднике с перечислением квалификаций
    FOR (Qualification q IS Qualification) AND mark(q) ORDER name(q) DO {
        lstQualification(o) <- IF lstQualification(o) THEN lstQualification(o) + ', '  ELSE '';
        lstQualification(o) <- lstQualification(o) + trim(name(q));
    }   
}