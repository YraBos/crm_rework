MODULE GridColOrderFunction;

REQUIRE Reflection, Utils;

NAMESPACE Reflection;

// см. Администрирование - Метаданные - Формы
// раздел определяет запись состояний табличного процессора

// Проблема: EXTEND FORM добавляет колонки, но изменить их порядок нельзя, 
// и в большей степени те, которые были определены ранее.
// При этом предполагается, что порядок следования должен задаваться настройками GRID, что возможно, невсегда удобно
// Предлагаемые функции рассчитаны на очистку и создание состояния грида по умолчанию для всех пользователей
// Порядок использования: onGridColOrderCls, onGrisColOrderSet .. onGrisColOrderSet, APPLY   

// Здесь:
// имя объекта, это имя получаемое в формах, как OBJECTS имя_объекта = Сущность
// имя формы, это имя формы, полученное как, Имя_модуля.имя_формы (см. Администрирование - Метаданные - Формы)
// имя свойства, это свойства, описываемого на форме, как PROPERTIES

// очищает предыдущие установки порядка следования колонок
// параметры: имя формы, имя объекта
onGridColOrderCls '' (fName,oName) {
    hasUserPreferences(GroupObject g) <- NULL WHERE sid(g) = oName AND canonicalName(form(g)) = fName; 
    columnOrder(PropertyDraw p) <- NULL WHERE isSubstring(sid(p),'(' + oName + ')')  AND canonicalName(form(p)) = fName;
    show(PropertyDraw p) <- NULL WHERE isSubstring(sid(p),'(' + oName + ')') AND canonicalName(form(p)) = fName;
} 


// устанавливает ордер для отдельно взятой колонки
// параметры: индекс колонки, имя формы, имя объекта, имя колонки
onGridColOrderAdd '' (ind,  fName, oName, cName) {
    columnOrder(PropertyDraw p) <- ind WHERE sid(p) = cName + '(' + oName + ')' AND canonicalName(form(p)) = fName;
    show(PropertyDraw p) <- PropertyDrawShowStatus.Show WHERE sid(p) = cName + '(' + oName + ')' AND canonicalName(form(p)) = fName;
    hasUserPreferences(GroupObject g) <- TRUE WHERE sid(g) = oName AND canonicalName(form(g)) = fName;
}


// ГЛАВНЫЙ метод установки полрядка следования колонок
// параметры: имя формы, имя объекта, имена свойств через запятую
// например, onGrisColOrderSet('Customer.customerTimeDashboard','t','in,date,count');
onGridColOrderSet '' (fName, oName, pNames) {
    NEWSESSION {
        LOCAL cNames    = TEXT (); cNames() <- '';
        LOCAL cIn       = STRING[100] (); cIn() <- '(' + oName + ')';
        LOCAL index     = INTEGER ();
        // формируем список всех полей
        FOR (PropertyDraw p IS PropertyDraw) AND canonicalName(form(p)) = fName AND isSubstring(sid(p),cIn()) DO {
            cNames() <- cNames() + getWord(sid(p),'(',1) + ',';
        } 
        onGridColOrderCls(fName, oName); // очищаем установки для объекта
        // поля, для которых важен Order    
        FOR iterate(INTEGER i,1,wordCount(pNames,',')) DO {
            index() <- i - 1; 
            onGridColOrderAdd(index(),fName,oName,getWord(pNames,',',i));
        }
        // прочие поля, где Order не важен
        FOR iterate(INTEGER i,1,wordCount(cNames(),',')) DO {
            IF NOT isSubstring(pNames, getWord(cNames(), ',', i)) THEN {
                index() <- index() + 1;
                onGridColOrderAdd(index(), fName, oName, getWord(cNames(), ',', i));
            }
        }        
        APPLY;
    }     
}

