MODULE ContractDashboardNew;

REQUIRE ContractDashboard, Debt, DebtDashboard, ContractNew, Time, Utils;

dateStart 'Начать с даты' = DATA DATE ();
qMonth 'Кол-во месяцев' = DATA INTEGER ();
skipDebt 'Пропускать ранее созданные' = DATA LOCAL BOOLEAN ();
oK 'Флаг удачного завершения' = DATA LOCAL BOOLEAN ();
seekDebt 'ID 1-й созданной задолженности' = DATA LOCAL Debt(); // только для редактирования договоров
inFilter '' = DATA LOCAL BOOLEAN (Contract);

FORM setProperties 'Создание задолженностей' 
    PROPERTIES() dateStart, qMonth, skipDebt
    EVENTS ON INIT {
        dateStart() <- currentDate();
        qMonth() <- 12 - extractMonthNumber(dateStart()) + 1;  // countMonthsDebt() - кол-во месяцев из настроек
        skipDebt() <- TRUE;
    }
    EVENTS ON CLOSE {
        qMonth() <- 0;
    }
;

// основной метод создания задолженностей по одному договору
onFillCreateDebts '' (Contract c) {
    LOCAL month, year, monthCur, yearCur = INTEGER ();
    LOCAL date, dateCur = DATE ();
    seekDebt() <- NULL; 
    oK() <- NULL;    
    // проверка: сумма ежемесячного платежа, день платежа, дата подписания клиентом
    IF NOT (sumMonth(c) AND paymentDay(c) AND dateSigningCustomer(c)) THEN RETURN;  
    IF NOT skipDebt() THEN DELETE Debt d WHERE contract(d) == c AND date(d) > dateStart();
//    DELETE Debt d WHERE contract(d) == c AND date(d) > currentDate();
    month()     <- extractMonthNumber(dateStart());
    year()      <- extractYear(dateStart());
    monthCur()  <- CASE WHEN NOT prevMonthCurrency(c) THEN month()
                        WHEN (prevMonthCurrency(c) AND (month() == 1)) THEN 12
                        ELSE month() (-) 1;
    yearCur() <- CASE WHEN monthCur() == 12 THEN year() (-) 1 ELSE year();
    FOR iterate(INTEGER i, 1, qMonth()) DO {
        TRY {
                date() <- toDateFormat(text(paymentDay(c)) + text(month()) + year(), 'DDMMYYYY');
                dateCur() <- toDateFormat(text(currencyDay(c)) + text(monthCur()) + yearCur(), 'DDMMYYYY');
        } CATCH {
                date() <- lastDayOfMonth(toDateFormat('01' + text(month()) + year(), 'DDMMYYYY'));
                dateCur() <- lastDayOfMonth(toDateFormat('01' + text(monthCur()) + yearCur(), 'DDMMYYYY'));
        }
       IF NOT debt(date(), c) THEN NEW d = Debt {
            IF NOT seekDebt() THEN seekDebt() <- d; 
            contract(d) <- c;
            date(d) <- date();
            IF inCurrency(c) THEN {
                sumCurrency(d) <- sumMonth(c);
                dataDateCurrency(d) <- dateCur();
            } ELSE {
                    sum(d) <- sumMonth(c);
            }
       }
       month() <- month() (+) 1; monthCur() <- monthCur() (+) 1;
       IF month() > 12 THEN {
            month() <- month() - 12; year() <- year() + 1;
       }
       IF monthCur() > 12 THEN {
            monthCur() <- monthCur() - 12; yearCur() <- yearCur() + 1;
       }
    }   // конец основного цикла FOR iterate
    oK() <- TRUE;
}

// кнопка на форме Задолженности
onSingleCreateDebts 'Создать задолженности' (Contract c) {
    IF NOT dateSigningCustomer(c) THEN {
        MESSAGE 'Не заполнено поле\nДата подписания клиентом';
        RETURN;
    }
    SHOW setProperties;
    IF NOT qMonth() OR qMonth() < 1 THEN {
        MESSAGE 'Неопределено количество месяцев\nВыполнение отменено'; 
        RETURN;
    }
    LOCAL cnt1, cnt2, month, year = INTEGER ();
    LOCAL date = DATE (); // дата, с которой выполняется расчет
    month() <- extractMonthNumber(dateStart()); year() <- extractYear(dateStart());
    TRY     date() <- toDateFormat(text(paymentDay(c)) + text(month()) + year(), 'DDMMYYYY'); 
    CATCH   date() <- lastDayOfMonth(toDateFormat('01' + text(month()) + year(), 'DDMMYYYY'));
    cnt1() <- GROUP SUM 1 IF contract(Debt d) = c AND date(d) < date();
    onFillCreateDebts(c); // Вызывает расчет задолженностей
    cnt2() <- GROUP SUM 1 IF contract(Debt d) = c AND date(d) < date();
    cnt1() <- OVERRIDE cnt1(), 0; cnt2() <- OVERRIDE cnt2(), 0;
    IF cnt1() != cnt2() THEN {
            MESSAGE 'Внимание. Обратите внимание!!!\n' + 
                    'Изменилось количество предыдущих задолженностей\n' + 
                    'до (' + cnt1() + ') и после (' + cnt2() + ') выполнения операции';
    }
    IF oK() AND seekDebt() THEN SEEK contract.d = seekDebt();
}

// кнопка на форме Работа с договорами
onFullCreateDebts 'Создать задолженности' () {
    LOCAL iAll, iOk = INTEGER();
    LOCAL cmsg, emsg = STRING();
    SHOW setProperties;
    inFilter(Contract o1) <- NULL;
    IF qMonth() = 0 THEN {
        MESSAGE 'Выполнение отменено'; 
        RETURN;
    }
    FOR [FILTER contractDashboard.o](Contract o2) DO inFilter(o2) <- TRUE;
    FOR (Contract c IS Contract) AND inFilter(c) AND nameType(c) = 'Сопровождение' AND NOT dateTo(c)  DO {
        iAll() <- iAll() (+) 1;
        onFillCreateDebts(c);
        IF NOT oK() THEN {
            IF emsg() THEN emsg() <- ', '; ELSE  emsg() <- '';
            IF length(emsg()) > 60 THEN emsg() <- emsg() + '\n';
            emsg() <- emsg() + name(customer(c));  
        } ELSE {iOk() <- iOk() (+) 1;}
    }
    cmsg() <-  'Всего договоров Сопровождения: ' + toChar(iAll(),'99999') + '\n' + 'Успешно обработано: ' + toChar(iOk(),'99999');
    IF emsg() THEN cmsg() <- cmsg() + 
        '\n----------------------------\n' + 
        'Необработанные договора по клиентам:\n' + emsg();
    MESSAGE cmsg();
    APPLY; 
}

EXTEND FORM contractDashboard 
    PROPERTIES(o) READONLY nameActTemplate, edc  
    PROPERTIES() onFullCreateDebts
; 

DESIGN contractDashboard {
    OBJECTS {
        NEW ocnt FIRST {
            horizontal = TRUE; // type = CONTAINERH;
            MOVE filter;
            NEW cntBut {
                caption = 'Доп. обработки';
                horizontal = TRUE; // type = CONTAINERH;
                MOVE PROPERTY (onFullCreateDebts());
                MOVE PROPERTY (recalcCurrencyDebts());
            }           
        }
    }
}

EXTEND FORM contract
    PROPERTIES(o) onSingleCreateDebts SHOWIF (sumMonth(o) AND paymentDay(o)) 
;

DESIGN contract {
    secondRow {
        MOVE PROPERTY (onSingleCreateDebts(o));
    }
}