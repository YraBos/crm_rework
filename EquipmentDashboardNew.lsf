MODULE EquipmentDashboardNew;

REQUIRE EquipmentDashboard, ContractSubject;

quantitySerialSubject 'По клиенту' (SubjectType st) = 
    GROUP SUM quantity(Subject cs) 
        IF Customer cst = filterCustomer()  AND name(type(cs)) = name(st)
            AND ( ( customer(Contract cnt) = cst AND contract(cs) = cnt ) 
                OR ( customer(Agreement agr) = cst AND agreement(cs) = agr ) )
;     


EXTEND FORM equipmentDashboard
    PROPERTIES quantitySerialSubject(t)
;

DESIGN equipmentDashboard {
    head {
        MOVE PROPERTY (quantitySerial(t)) {
            caption = 'Сер. номеров, выдано:';
        }
        MOVE PROPERTY(quantitySerialSubject(t)) {
            caption = 'Сер. номеров, всего:';
        }
    }
}

// --- задач - 13003
numberChange 'Замена номера' = DATA ISTRING[250] (EquipmentSerial) CHARWIDTH 15;
note 'Примечание' = DATA STRING[200] (EquipmentSerial);

FORM editNumberChange 'Редактирование замен'
    OBJECTS n = EquipmentSerial PANEL  
    PROPERTIES(n) READONLY number
    PROPERTIES(n) numberChange, note 
    EDIT EquipmentSerial OBJECT n
; 

createdDate 'Дата' (EquipmentSerial s) = createdDate(equipment(s)); // дата создания ключа
 
FORM repOutSerials 'Данные'
    OBJECTS c = Customer PROPERTIES name(c), currentDate() PANEL
    OBJECTS l = LegalEntity PROPERTIES(l) name PANEL
    OBJECTS s = EquipmentSerial PROPERTIES(s) createdDate, number, numberChange, note
    FILTERS customer(l) = c, legalEntity(contract(equipment(s))) = l
    ORDERS name(l), createdDate(s) 
;

onRepOutSerials 'Данные' (Equipment e) {
    PRINT repOutSerials OBJECTS c=customer(e); // XLS - отладка
}

EXTEND FORM equipmentDashboard
    PROPERTIES(n) READONLY numberChange, note
    PROPERTIES(n) NEWSESSION EDIT
    PROPERTIES(e) onRepOutSerials 
;

DESIGN equipmentDashboard {
    head {
        MOVE PROPERTY (onRepOutSerials(e));
    }
}