MODULE EquipmentDashboardNew;

REQUIRE EquipmentDashboard, ContractSubject, CustomerAttachment;

// --- задач - 13003, 12704
numberChange 'Замена номера' = DATA ISTRING[250] (EquipmentSerial) CHARWIDTH 15;
dateChange 'Дата замены' = DATA DATE (EquipmentSerial);
note 'Примечание' = DATA STRING[200] (EquipmentSerial);

FORM editNumberChange 'Редактирование замен'
    OBJECTS n = EquipmentSerial PANEL  
    PROPERTIES(n) number, numberChange, dateChange, note 
    EDIT EquipmentSerial OBJECT n
    EVENTS ON INIT {
        IF NOT numberChange(n) THEN {
            numberChange(n) <- number(n);
            number(n) <- NULL; 
        }
        dateChange(n) <- currentDate(); 
    }
; 

DESIGN editNumberChange {
   BOX(n) {
        MOVE PROPERTY (number(n)) {caption = 'Новый номер';} 
        MOVE PROPERTY (dateChange(n));
        MOVE PROPERTY (numberChange(n)) {caption = 'Старый номер';}
        MOVE PROPERTY (note(n)); 
   } 
}

createdDate 'Дата' (EquipmentSerial s) = createdDate(equipment(s)); // дата создания ключа
nameLegalEntity 'Название организации' (EquipmentSerial s) = OVERRIDE ' ' + name(legalEntity(contract(equipment(s)))), '* Не связанные с договором';
sumCustomer 'Итого по клиенту' (Customer c, SubjectType t) = GROUP SUM 1 IF subjectType(equipment(EquipmentSerial s)) = t BY customer(equipment(s));  
 
FORM repOutSerials 'Данные'
    OBJECTS c = Customer PROPERTIES name(c) PANEL
    OBJECTS t = SubjectType PROPERTIES name(t), sumCustomer(c, t) PANEL
    OBJECTS s = EquipmentSerial PROPERTIES(s) nameLegalEntity, createdDate, number, dateChange, numberChange, note
    FILTERS customer(equipment(s)) = c, subjectType(equipment(s)) = t
    ORDERS nameLegalEntity(s),createdDate(s) 
;

onRepOutSerials 'Данные' (Equipment e,SubjectType t) {
    IF NOT filterCustomer() THEN {
        MESSAGE 'Определите клиента';
        RETURN ;
    } 
    IF NOT name(t) = 'LST ТСД' THEN {
    // просто отобразим отчет и выйдем
        PRINT repOutSerials OBJECTS c = customer(e),t = t XLS; // XLS;
        RETURN;
    }
    // формируем отчет и пишим в документы по клиенту
    LOCAL frep = FILE();
    PRINT repOutSerials OBJECTS c = customer(e),t = t XLS TO frep; 
    LOCAL ot = Attachment();
    ot() <- GROUP MAX Attachment a IF customer(a) = customer(e) AND name(type(a)) = 'Список ключей LS Trade ТСД';
    IF NOT ot() THEN NEW a = Attachment {
        ot() <- a;
        customer(a) <- customer(e);
        type(a) <- GROUP MAX AttachmentType at IF name(at) = 'Список ключей LS Trade ТСД';
    }
    date(ot()) <- currentDate();
    file(ot()) <- frep();
    open(file(ot()));
    APPLY;
}

fltNumber 'Фильтр по номеру' = DATA LOCAL STRING[15] () CHARWIDTH 15; 
// вычисление ID equipment от значения ключа оборудования
exprEquipment '' (ISTRING[250] num) = GROUP MAX equipment(EquipmentSerial n1) IF number(n1) = num;
// сброс выражения фильтра
clsExprEquipment 'X' () {fltNumber() <- NULL;}

EXTEND FORM equipmentDashboard
    PROPERTIES(n) READONLY dateChange, numberChange, note
    PROPERTIES(n) NEWSESSION EDIT
    PROPERTIES onRepOutSerials(e,t)
    PROPERTIES fltNumber(), clsExprEquipment()
    FILTERS IF fltNumber() THEN e = exprEquipment(fltNumber()) ELSE TRUE
    FILTERS IF fltNumber() THEN number(n) = fltNumber() ELSE TRUE
;

DESIGN equipmentDashboard {
    head {
        MOVE PROPERTY (onRepOutSerials(e,t));
    }
    TOOLBARRIGHT(e) {
        MOVE PROPERTY (fltNumber()) {marginLeft = 10;};
        MOVE PROPERTY (clsExprEquipment());
    } 
}
