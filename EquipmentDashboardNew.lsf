MODULE EquipmentDashboardNew;

REQUIRE EquipmentDashboard, ContractSubject;

quantitySerialSubject 'По клиенту' (SubjectType st) = 
    GROUP SUM quantity(Subject cs) 
        IF Customer cst = filterCustomer()  AND name(type(cs)) = name(st)
            AND ( ( customer(Contract cnt) = cst AND contract(cs) = cnt ) 
                OR ( customer(Agreement agr) = cst AND agreement(cs) = agr ) )
;     

EXTEND FORM equipmentDashboard
    PROPERTIES(t) quantitySerialSubject
;

DESIGN equipmentDashboard {
    head {
        MOVE PROPERTY (quantitySerial(t)) {
            caption = 'Сер. номеров, выдано:';
        }
        MOVE PROPERTY(quantitySerialSubject(t)) {
            caption = 'Сер. номеров, всего:';
        }
    }
}

// --- задач - 13003
numberChange 'Замена номера' = DATA ISTRING[250] (EquipmentSerial) CHARWIDTH 15;
note 'Примечание' = DATA STRING[200] (EquipmentSerial);

FORM editNumberChange 'Редактирование замен'
    OBJECTS n = EquipmentSerial PANEL  
    PROPERTIES(n) READONLY number
    PROPERTIES(n) numberChange, note 
    EDIT EquipmentSerial OBJECT n
; 

createdDate 'Дата' (EquipmentSerial s) = createdDate(equipment(s)); // дата создания ключа

legalEntity 'Организация' (EquipmentSerial s) = legalEntity(contract(equipment(s)));
sumLegalEntity 'Итого по организации' (LegalEntity l, SubjectType t) = GROUP SUM 1 IF subjectType(equipment(EquipmentSerial s)) = t BY legalEntity(s); 
sumCustomer 'Итого по клиенту' (Customer c, SubjectType t) = GROUP SUM 1 IF subjectType(equipment(EquipmentSerial s)) = t BY customer(equipment(s));  
 
FORM repOutSerials 'Данные'
    OBJECTS c = Customer PROPERTIES name(c), currentDate() PANEL
    OBJECTS t = SubjectType PROPERTIES name(t) PANEL
    OBJECTS l = LegalEntity PROPERTIES name(l), sumLegalEntity(l, t), sumCustomer(c, t) PANEL
    OBJECTS s = EquipmentSerial PROPERTIES(s) createdDate, number, numberChange, note
    FILTERS customer(l) = c, legalEntity(contract(equipment(s))) = l
    FILTERS subjectType(equipment(s)) = t
    ORDERS name(l), createdDate(s) 
;

onRepOutSerials 'Данные' (Equipment e,SubjectType t) {
    IF NOT filterCustomer() THEN {
        MESSAGE 'Определите клиента';
        RETURN ;
    } 
    PRINT repOutSerials OBJECTS c = customer(e),t = t XLS; // XLS - отладка
}

fltNumber 'Фильтр по номеру' = DATA LOCAL STRING[15] () CHARWIDTH 15; 
// вычисление ID equipment от значения ключа оборудования
exprEquipment '' (ISTRING[250] num) = GROUP MAX equipment(EquipmentSerial n1) IF number(n1) = num;
// сброс выражения фильтра
clsExprEquipment 'X' () {fltNumber() <- NULL;}

EXTEND FORM equipmentDashboard
    PROPERTIES(n) READONLY numberChange, note
    PROPERTIES(n) NEWSESSION EDIT
    PROPERTIES onRepOutSerials(e,t)
    PROPERTIES fltNumber(), clsExprEquipment()
    FILTERS IF fltNumber() THEN e = exprEquipment(fltNumber()) ELSE TRUE
    FILTERS IF fltNumber() THEN number(n) = fltNumber() ELSE TRUE
;

DESIGN equipmentDashboard {
    head {
        MOVE PROPERTY (onRepOutSerials(e,t));
    }
    TOOLBARRIGHT(e) {
        MOVE PROPERTY (fltNumber()) {marginLeft = 10;};
        MOVE PROPERTY (clsExprEquipment());
    } 
}

// --- задач - 12671