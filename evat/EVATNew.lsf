MODULE EVATNew;

REQUIRE EVAT, ActEVAT;

NAMESPACE EVAT;

// Сдвигаем кнопку создать ЭСЧФ линию панели инструментов грида
DESIGN ActEVATs {
    BOX(a) {
        TOOLBAR (a) {
            MOVE PROPERTY (generateEVAT());
        }
    }
}

// Печатная форма, стиль отображения, как на портале
FORM viewEvat 'Просмотр ЭСЧФ'
    OBJECTS e=EVAT PANEL
    PROPERTIES(e) READONLY documentType, exportNumber, dateDoc, dependentPersonSupplier, 
                  residentsOfOffshoreSupplier, specialDealGoodsSupplier, bigCompanySupplier,
                  countryCodeSupplier, unpSupplier, branchCodeSupplier, nameSupplier, addressSupplier, 
                  countryCodeCustomer, unpCustomer, branchCodeCustomer, nameCustomer, addressCustomer,
                  numberContract, dateContract
    OBJECTS d = EVATDetail
    PROPERTIES(d) READONLY name, code, codeOced, quantity, price, sum, exciseSum, 
                           vatRate, vatSum, sumWithVAT, captionDescriptionType
    FILTERS EVAT(d) == e
;

onReportEvat 'Показать СФ' (EVAT o) {
    PRINT viewEvat OBJECTS e = o;
} TOOLBAR ;

EXTEND FORM EVATs
    PROPERTIES(e) onReportEvat
;

// Редактирование нименования услуг
FORM editNameEvatDetail 'Редактирование услуг'
    OBJECTS d = EVATDetail PANEL 
    PROPERTIES(d) name
    EDIT EVATDetail OBJECT d
;

DESIGN editNameEvatDetail {
    OBJECTS {
        NEW cnt1 {
            horizontal = TRUE ; // type = CONTAINERH;
            fill = 1;
            caption = 'Наименование товаров (работ, услуг)';
            MOVE PROPERTY (name(d)) {caption = ''; fontSize = 16;}
        }
    }
}


onEditDetailName 'Изменить Услуги' (EVATDetail o) {
  NEWSESSION SHOW editNameEvatDetail OBJECTS d = o WAIT DOCKED ;
} TOOLBAR ;

EXTEND FORM EVATs
    PROPERTIES(d) onEditDetailName
;

DESIGN EVATs {
    BOX(dates) {
        horizontal = TRUE ; // type = CONTAINERH ;
    }
}