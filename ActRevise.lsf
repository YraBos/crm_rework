MODULE ActRevise;

REQUIRE PaymentProcessDashboardNew, LegalEntityBy, DocWord;

// для формы
actReviseDS     'Начало периода'            = DATA LOCAL DATE ();
actReviseDE     'Конец периода'             = DATA LOCAL DATE ();
actReviseCN     'Контракт'                  = DATA LOCAL Contract ();
actReviseDBSum  'Дебит на начало'           = DATA LOCAL NUMERIC[14,2] (); 
actReviseCRSum  'Кредит на начало'          = DATA LOCAL NUMERIC[14,2] ();
actReviseSum    'Учитывать остатки'         = DATA LOCAL BOOLEAN ();
actReviseScreen 'Выводить отчет на экран'   = DATA LOCAL BOOLEAN ();

// Заголовки и отд. свойства отчета
actReviseTitle1     '' = DATA LOCAL TEXT          ();    
actReviseTitle2     '' = DATA LOCAL TEXT          ();   
actReviseTitle3     '' = DATA LOCAL TEXT          ();   
actReviseTitle4     '' = DATA LOCAL TEXT          ();   
actReviseTitle5     '' = DATA LOCAL TEXT          ();   
actReviseTitle6     '' = DATA LOCAL TEXT          ();   
actReviseTitle7     '' = DATA LOCAL TEXT          ();  // сальдо на конец   
actReviseText       '' = DATA LOCAL STRING[100]   ();  // Связанные документы 
 
// временный класс строк класс отчета 
CLASS ActReviseTemp;
nid     'Индекс'    = DATA INTEGER          (ActReviseTemp); 
number  'Документ'  = DATA STRING[100]      (ActReviseTemp);
date    'Дата'      = DATA DATE             (ActReviseTemp);
note    'Примечание'= DATA TEXT             (ActReviseTemp);
debit   'Дебет'     = DATA NUMERIC[10,2]    (ActReviseTemp);
credit  'Кредит'    = DATA NUMERIC[10,2]    (ActReviseTemp);

FORM actRevise 'Акт сверки'
    OBJECTS ds = DATE PANEL, de = DATE PANEL, 
            sDBSum = NUMERIC[14,2] PANEL, sCRSum = NUMERIC[14,2] PANEL, 
            le = LegalEntity PANEL, cn = Contract PANEL          
    PROPERTIES ds 'Дата с' = VALUE(ds), de 'по' = VALUE(de)
    PROPERTIES design1 = name(le) SELECTOR 
    PROPERTIES design2 = number(cn) SELECTOR
    PROPERTIES(cn) READONLY date, nameType
    PROPERTIES sDBSum 'Дебит' = VALUE(sDBSum), sCRSum 'Кредит' = VALUE(sCRSum), actReviseSum(), actReviseScreen() 
    FILTERS legalEntity(cn) = le
    EVENTS ON INIT {
        SEEK actRevise.ds = toDateFormat('0101' + toChar(extractYear(currentDate()),'9999'), 'DDMMYYYY');
        SEEK actRevise.de = currentDate();
        SEEK actRevise.le = legalEntity(cn);
        SEEK actRevise.sDBSum = 0.00;
        SEEK actRevise.sCRSum = 0.00;
    }
    EVENTS ON OK {
        actReviseDS() <- ds; actReviseDE() <- de; actReviseCN() <- cn; 
        actReviseDBSum() <- sDBSum;
        actReviseCRSum() <- sCRSum;
    }
;

DESIGN actRevise {
    TOOLBARBOX {
        TOOLBARLEFT {
            MOVE PROPERTY (actReviseScreen()) {marginTop = 8;};
        }
    }   
    OBJECTS {
        NEW cntMain {
            type = CONTAINERV;
            NEW cntDate {
                caption = 'выбор период расчета';
                type = CONTAINERH;
                MOVE PROPERTY (ds);
                MOVE PROPERTY (de) {marginLeft = 10;};
            }
            NEW cntLE {
                caption = 'выбор организации';
                MOVE PROPERTY (design1) {charWidth = 50;};
            }
            NEW cntCNT {
                caption = 'выбор договора';
                type = CONTAINERH;  
                MOVE PROPERTY (design2);
                MOVE PROPERTY (date(cn)) {marginLeft = 10;}; 
                MOVE PROPERTY (nameType(cn)) {marginLeft = 10;}; 
            }
            NEW cntSaldo{
                caption = 'Остатки на начало';
                type = CONTAINERH;
                MOVE PROPERTY (sDBSum) ;
                MOVE PROPERTY (sCRSum) {marginLeft = 10;};
                MOVE PROPERTY (actReviseSum()) {marginLeft = 10;};
            }
        }
    }    
}

// печатная форма
FORM reportActRevise 'Печать Акта сверки'
    PROPERTIES() actReviseTitle1, actReviseTitle2, actReviseTitle3, actReviseTitle4, actReviseTitle5, actReviseTitle6, actReviseTitle7 PANEL 
    OBJECTS a = ActReviseTemp
    PROPERTIES(a) nid, date, number, note, debit, credit
    ORDERS nid(a), date(a)
;

// чистим совйства и временную таблицу
onActReviseCls 'Очистка свойств' () {
    DELETE ActReviseTemp a WHERE a IS ActReviseTemp; // чистим tmp таблицу
    actReviseCN() <- NULL;  
    actReviseTitle1() <- NULL; actReviseTitle2() <- NULL; actReviseTitle3() <- NULL; actReviseTitle4() <- NULL; 
    actReviseTitle5() <- NULL; actReviseTitle6() <- NULL; actReviseTitle7() <- NULL; 
}

// расчет акта для печатной формы
onActRevise 'Акт сверки' (Payment o) {
    onActReviseCls();
    DIALOG actRevise OBJECTS cn = contract(o);
    IF NOT actReviseCN() THEN RETURN;
    LOCAL sdS, sdE, skS, skE = NUMERIC[14,5] (); // обороты дебет начало-конец, кредит начало-конец
    IF actReviseSum() THEN {
        sdS() <- actReviseDBSum();
        sdE() <- GROUP SUM sum(Act a) IF contract(a) = actReviseCN() AND date(a) >= actReviseDS() AND date(a) <= actReviseDE(); 
        sdE() <- sdS() + (OVERRIDE sdE(), 0.00);
    } ELSE {
        sdS() <- GROUP SUM sum(Act a) IF contract(a) = actReviseCN() AND date(a) <  actReviseDS(); 
        sdS() <- OVERRIDE sdS(), 0.00; 
        sdE() <- GROUP SUM sum(Act a) IF contract(a) = actReviseCN() AND date(a) <= actReviseDE(); 
        sdE() <- OVERRIDE sdE(), 0.00;
    }
    FOR (Act a IS Act) AND contract(a) = contract(actReviseCN()) AND (date(a) > actReviseDS() AND date(a) <= actReviseDE()) DO {
        NEW ob = ActReviseTemp {
            nid(ob)     <- 2;
            date(ob)    <- date(a);
            number(ob)  <- number(a);
            note(ob)    <- text(a);
            debit(ob)   <- round(sum(a),2);
        }
    }  
    IF actReviseSum() THEN {
        skS() <- actReviseCRSum();
        skE() <- GROUP SUM sum(Payment p) IF contract(p) = actReviseCN() AND date(p) >= actReviseDS() AND date(p) <= actReviseDE(); 
        skE() <- skS() + (OVERRIDE skE(), 0.00);
    } ELSE {
        skS() <- GROUP SUM sum(Payment p) IF contract(p) = actReviseCN() AND date(p) <  actReviseDS();
        skS() <- OVERRIDE skS(), 0.00;
        skE() <- GROUP SUM sum(Payment p) IF contract(p) = actReviseCN() AND date(p) <= actReviseDE();
        skE() <- OVERRIDE skE(), 0.00;
    }
    FOR (Payment p IS Payment) AND contract(p) = contract(actReviseCN())  AND (date(p) > actReviseDS() AND date(p) <= actReviseDE()) DO {
        NEW ob = ActReviseTemp {
            nid(ob)     <- 2;
            date(ob)    <- date(p);
            number(ob)  <- OVERRIDE number(p),'б/н';
            note(ob)    <- note(p);
            credit(ob)  <- round(sum(p),2);         
        }
    }
    // сальдо и обороты
    NEW ob = ActReviseTemp {
       nid(ob)    <- 1;
       note(ob)   <- 'Сальдо на ' + toDateDDMMYYYY(actReviseDS()) + 'г., бел. рубли';
       IF      sdS() - skS() > 0 THEN debit(ob)  <- round(sdS() - skS(),2);
       ELSE IF sdS() - skS() < 0 THEN credit(ob) <- round(skS() - sdS(),2);
    }
    NEW ob = ActReviseTemp {
       nid(ob)    <- 3;
       note(ob)   <- 'Обороты за период \nc ' + toDateDDMMYYYY(actReviseDS()) + ' по ' + toDateDDMMYYYY(actReviseDE()) + 'г.';
       debit(ob)  <- IF sdE() - sdS() != 0 THEN round(sdE() - sdS(),2);
       credit(ob) <- IF skE() - skS() != 0 THEN round(skE() - skS(),2);
    }
    // заголовки и итоги
    LOCAL ole1, ole2 = LegalEntity ();
    LOCAL gbux, gdir = STRING ();
    gbux() <- 'Лангуева И.И.'; gdir() <- 'Донсков Е.С.'; 
    ole1() <- GROUP MAX (LegalEntity l) IF id(l) = '1'; // НТ ООО ЛюксСофт
    ole2() <- legalEntity(contract(actReviseCN()));     // Организация
    actReviseTitle1() <- OVERRIDE fullName(ole1()), name(ole1());
    actReviseTitle2() <- OVERRIDE fullName(ole2()), name(ole2());
    actReviseTitle3() <- 
        'взаимных расчетов за период с ' + toDateDDMMYYYY(actReviseDS()) + ' по ' + toDateDDMMYYYY(actReviseDE()) + 'г.\n' +
        'между ' + actReviseTitle1() + ' и ' + actReviseTitle2() + '\n' +
        'по договору № '   + number(contract(actReviseCN())) + ' от ' + toDateDDMMYYYY(date(contract(actReviseCN()))) + 'г.\n';
    actReviseTitle4() <- 
        '   Мы, нижеподписавшиеся, гл. бухгалтер ' + actReviseTitle1() + ' ' + gbux() + ' ' + 
        'с одной стороны, и ' + actReviseTitle2() + ' с другой стороны, составили настоящий акт сверки в том, ' + 
        'что состояние взаимных расчетов по данным учета следующее:\n';
    actReviseTitle5() <-
        (IF UNP(ole1()) THEN 'УНП: ' + UNP(ole1()) + '\n' ELSE '') +    
        (IF nameLegalAddress(ole1()) THEN nameLegalAddress(ole1()) + '\n' ELSE '') + '\n' +
        'директор      ______________________ (' + gdir() + ')\n\n' +
        'гл. бухгалтер ______________________ (' + gbux() + ')\n\n' + 
        '"____" _________________ ' + toChar(extractYear(actReviseDE()),'9999') + 'г.\n\n' + 
        'В случае невозврата акта сверки в течении 10 дней,\n' +
        'сальдо считается подтвержденным';
    actReviseTitle6() <-
        (IF UNP(ole2()) THEN 'УНП: ' + UNP(ole2()) + '\n' ELSE '') +    
        (IF nameLegalAddress(ole2()) THEN nameLegalAddress(ole2()) + '\n' ELSE '') + '\n' + 
        'руководитель  ______________________ (_______________)\n\n' + 
        'гл. бухгалтер ______________________ (_______________)\n\n' + 
        '"____" _________________ ' + toChar(extractYear(actReviseDE()),'9999') + 'г.\n';
    // задолженность на конец
    actReviseTitle7()   <- 'Задолженность ';  
    NEW ob = ActReviseTemp {
       nid(ob)    <- 4;
       note(ob) <- 'Сальдо исходящие, бел. рубли';
       IF sdE() - skE() > 0 THEN {
            debit(ob)  <- round(sdE() - skE(),2);
            actReviseTitle7() <- 
                'Задолженность ' + actReviseTitle2() + ' перед ' + actReviseTitle1() + ' составляет ' + trim(toChar(debit(ob),'9999999999.99')) + 
                ' (' + numSpelledCurrency(debit(ob), '00d', currency(actReviseCN())) + ')';  
       } ELSE IF sdE() - skE() < 0 THEN {
            credit(ob) <- round(skE() - sdE(),2);
            actReviseTitle7() <- 
                'Задолженность ' + actReviseTitle1() + ' перед ' + actReviseTitle2() + ' составляет ' + trim(toChar(credit(ob),'9999999999.99')) +  
                ' (' + numSpelledCurrency(credit(ob), '00d', currency(actReviseCN())) + ')';  
       } ELSE actReviseTitle7() <- 'Задолженности нет'; 
    }
    IF actReviseScreen() THEN PRINT reportActRevise; ELSE PRINT reportActRevise XLS;
    CANCEL; 
} TOOLBAR ;


EXTEND FORM paymentProcessDasboard
    PROPERTIES onActRevise(p) TOOLBAR 
;
