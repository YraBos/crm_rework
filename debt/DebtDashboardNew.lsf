MODULE DebtDashboardNew;

REQUIRE DebtDashboard, Payment, Utils, Time, CustomerNew, AgreementNew, DocsInWork, EmployeeNew, RevisionHelp, ActWordValue;

filterSumDebt 'Размер задолженности' = DATA LOCAL INTEGER ();

sumContract 'Сумма договора' (Debt d) = round(sum(contract(d)),2);
sumAgreement 'Сумма соглашения' (Debt d)  = round(sum(agreement(d)),2);
dateContract 'Дата договора' (Debt d) = date(contract(d));
dateAgreement 'Дата соглашения' (Debt d) = date(agreement(d));
//balance 'Остаток' (Debt d) = round(sum(d) - IF paid(d) THEN paid(d) ELSE 0.00, 2);
balance 'Остаток' (Debt d) = IF NOT inactive(d) THEN round(sum(d) (-) paid(d),2) ELSE 0.00;
numberAct 'Номер акта' (Debt d) = number(act(d));
dateAct 'Дата акта' (Debt d) = date(act(d));
nameAct 'Название акта' (Debt d) = name(act(d));
sumAct 'Сумма акта' (Debt d) = sum(act(d));

dateContractISO '' (Debt d) = toDateISO(dateContract(d));
agrFlag '' (Debt d) = IF agreement(d) THEN 1 ELSE 0;
actFlag '' (Debt d) = IF act(d) THEN 1 ELSE 0;

// связано с валютными договорами
inCurrency 'USD' (Debt d) = IF inCurrency(contract(d)) THEN 1 ELSE 0;

clientTypeDebt 'Тип клиента' (Debt d) = clientType(customer(d));

FORM reportDebs 'Печать'
    OBJECTS dd = DATE PANEL
    PROPERTIES VALUE (dd)
    OBJECTS c  = Customer PROPERTIES(c) name PANEL
    OBJECTS t  = ContractType PROPERTIES(t) name PANEL
    OBJECTS db = Debt
    PROPERTIES(db) date, nameLegalEntity, 
                   sum, paid, balance,                              // рублевый
                   numberContract, dateContract, nameContract, sumContract, dateContractISO,
                   numberAgreement, dateAgreement, nameAgreement, sumAgreement,
                   numberAct, dateAct, nameAct, sumAct,
                   agrFlag, actFlag, inCurrency 
    PROPERTIES sumDebt(c,dd)
    FILTERS customer(db) == c, date(db) <= dd, type(contract(db)) == t
    FILTERS abs(balance(db)) > 0
    FILTERS sumDebt(c, dd)
    FILTERS IF filterCustomerContractType() THEN t == filterCustomerContractType() ELSE TRUE 
    FILTERS IF filterSumDebt() THEN abs(sum(db) (-) paid(db)) > filterSumDebt() ELSE TRUE  
    FILTERS NOT inactive(db)  
    ORDERS nameLegalEntity(db),dateContractISO(db),numberContract(db),date(db)
;

onReport 'Отчет' (Customer c, DATE dd) {
    PRINT reportDebs OBJECTS c = c, dd = dd XLS; // без XLS - отладка
}

EXTEND FORM debtDashboard
    PROPERTIES(d) READONLY clientTypeDebt AFTER nameCustomer(d)
    PROPERTIES READONLY clientType(c) AFTER name(c)
    OBJECTS le = LegalEntity
    PROPERTIES filterSumDebt(), onReport(c, dd), onOpenHelp()
//    PROPERTIES onRepDocsInWork()                                // Документы в работе
    EVENTS ON INIT {
        filterSumDebt() <- 100;
    }
    FILTERS customer(le) = c
//    FILTERS IF filterSumDebt() THEN abs(sumDebt(c,t,dd)) > filterSumDebt() ELSE TRUE  
;

DESIGN debtDashboard {
    BOX (dd) {
        caption = 'Фильтры и отчеты';
        MOVE PROPERTY (filterSumDebt()) {marginLeft = 10;};
        MOVE PROPERTY (onReport(c, dd));
//        MOVE PROPERTY (onRepDocsInWork());                      // Документы в работе
    }
}

// RM15937, вытянуть тип акта из договоора
nameActTemplateDebt 'Шаблон акта' (Debt d) = nameActTemplate(contract(d));
EXTEND FORM debtDashboard
    PROPERTIES(d) nameActTemplateDebt 
; 

// RM16613, доб. название валюты, сделал по аналогии с sumDebt 
nameCurrencyCustomer 'Валюта' (Customer c, ContractType t, DATE d) = 
    GROUP MAX shortName(currency(Debt dd)) IF date(dd) <= d BY customer(dd), type(contract(dd));

EXTEND FORM debtDashboard
    PROPERTIES READONLY nameCurrencyCustomer(c, t, dd) 
;

// RM23326, RM24026
EXTEND FORM debtDashboard
    OBJECTS p = Payment
    PROPERTIES (p) READONLY date, nameLegalEntity, nameType, sum, shortNameCurrency, descriptionContract
    ORDERS date(p) DESC 
    FILTERS customer(p) == c
;

DESIGN debtDashboard {
    right {
        MOVE BOX(p) AFTER BOX (db);
    }
}

//RM24026 - отметка неактивных задолженностей
fltInactive 'Неактивные' = DATA LOCAL BOOLEAN ();

FORM editInactive 'Изменение активности' OBJECTS valueInactive = BOOLEAN PANEL NULL 
    PROPERTIES valueInactive 'Активность' = VALUE(valueInactive)
;

EXTEND FORM debtDashboard 
    PROPERTIES fltInactive() 
    PROPERTIES(db) READONLY inactive FIRST
    FILTERS IF fltInactive() THEN TRUE ELSE NOT inactive(db);  

onChangeInactive 'Активность' (Debt o) {
    IF NOT killDebt(currentUser()) THEN { MESSAGE 'Недостаточно прав для выполнения операции'; RETURN; }
    fltInactive() <- TRUE;
    DIALOG editInactive OBJECTS valueInactive = inactive(o) INPUT DO NEWSESSION {
        inactive(o) <- valueInactive;
        APPLY; 
    }       
}

EXTEND FORM debtDashboard PROPERTIES(db) SHOWIF killDebt(currentUser()) onChangeInactive;    

DESIGN debtDashboard { 
    PROPERTY(inactive(db)) {caption = 'ФЗ';} 
    TOOLBARRIGHT (db) {
        MOVE PROPERTY (fltInactive());
        MOVE PROPERTY (onChangeInactive(db)); 
    }
}

// RM24675, Добавить подписанта, см. ActWordValue
EXTEND FORM requiredTemplateEntries
    PROPERTIES signerName() SELECTOR
    EVENTS ON INIT { ActWordValue.signerId() <- NULL; }
;

DESIGN requiredTemplateEntries {
    PROPERTY (signerName()) {charWidth = 40;}
}

overPayment 'П' (Debt o, DATE d2) = IF paid(o,d2) > sum(o) THEN '+';
EXTEND FORM debtDashboard 
    PROPERTIES READONLY BACKGROUND IF paid(db,dd) > sum(db) THEN RGB(255, 204, 255) overPayment(db,dd) AFTER inactive(db)
; 

inACR 'АСР' = DATA BOOLEAN (Subject); // Включать в акты АСР

EXTEND FORM contract 
    PROPERTIES (s)  inACR SHOWIF nameType(o) = 'Сопровождение'
    PROPERTIES (as) inACR SHOWIF nameType(o) = 'Сопровождение'
;

EXTEND FORM agreement
    PROPERTIES (s) inACR SHOWIF nameType(contractAgreement(s)) = 'Сопровождение' 
;    

debtCustomer 'Клиент' (Debt o) = name(customer(o));
FORM inactive 'Неактивные задолженности'
    OBJECTS d = Debt
    PROPERTIES(d) READONLY inactive, debtCustomer, date, sum, numberContract
    PROPERTIES(d) onChangeInactive SHOWIF killDebt(currentUser())  
    FILTERS inactive(d)
    ORDERS debtCustomer(d),numberContract(d),date(d)
;

showInactive 'Неактивные задолженности' () { SHOW inactive; }

EXTEND FORM debtDashboard
    PROPERTIES showInactive() TOOLBAR;