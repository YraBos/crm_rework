MODULE ActWordNew;

REQUIRE ActWord, Time, Utils, ActWordValue, DebtDashboardNew;

NAMESPACE Act;

chkData (ContractSubject s,Act a) = IF contract(s) = contract(a) AND inACR(s) THEN TRUE;
chkData (AgreementSubject s,Act a) = IF contractAgreement(s) = contract(a) AND inACR(s) THEN TRUE;
num2str (NUMERIC[14,3] n1) = IF n1 THEN rtrim(rtrim(TEXT(n1),'0'),'.') ELSE '';
usdWords(NUMERIC[14,2] n1) = upperFirstChar(numSpelled(n1, 'M', 'доллар США,доллара США,долларов США', 'F', 'цент,цента,центов', '00d'));

//AFTER createActTemplate (Act ag, Template t) DO {
overPrintActWord (Act ag, Template t) + {
    value(TemplateEntry d) <- TEXT (
        lower(extractMonthName(toDate(ag))) + toChar(extractYear(toDate(ag)),'9999') + ' года'
    ) WHERE key(d) = 'NAME_ACT_MONTH' AND template (d) == t;
    IF ActWordValue.signerId() THEN { // смена подписанта 
        value(TemplateEntry d) <- OVERRIDE shortName(ActWordValue.signerId()), '' WHERE key(d) = 'SHORT_NAME_AUTHORISED_PERSON_COMPANY' AND template (d) == t;
        value(TemplateEntry d) <- OVERRIDE namePosition(ActWordValue.signerId()), '' WHERE key(d) = 'POSITION_AUTHORISED_PERSON_COMPANY' AND template (d) == t;
    }
    IF nameType(ag) = 'АСР' THEN {
        // Предмет договора: все свойства рассчитываются относительно галки АСР
        LOCAL storeQuantity,storePrice,storeSum = STRING ();
        LOCAL storeQuantityTotal, storeSumTotal, procLisence = NUMERIC[14,2] ();
        LOCAL debt = Debt();
        FOR (ContractSubject s IS ContractSubject) AND chkData(s, ag) AND NOT isISubstring(nameType(s),'Процент') ORDER s DO {
            storeQuantity()         <- (IF storeQuantity() THEN storeQuantity() + '\n\n' ELSE '') + toChar(quantity(s),'99999999.99');
            storePrice()            <- (IF storePrice() THEN storePrice() + '\n\n' ELSE '') + toChar(price(s),'99999999.99');
            storeSum()              <- (IF storeSum() THEN storeSum() + '\n\n' ELSE '') + toChar(sum(s),'99999999.99');
            storeQuantityTotal()    <- storeQuantityTotal() (+) quantity(s);
            storeSumTotal()         <- storeSumTotal() (+) sum(s);
        }
        FOR (AgreementSubject s IS AgreementSubject) AND chkData(s, ag) AND NOT isISubstring(nameType(s),'Процент') AND NOT storeQuantity() ORDER s DO {
            storeQuantity()         <- (IF storeQuantity() THEN storeQuantity() + '\n\n' ELSE '') + toChar(quantity(s),'99999999.99');
            storePrice()            <- (IF storePrice() THEN storePrice() + '\n\n' ELSE '') + toChar(price(s),'99999999.99');
            storeSum()              <- (IF storeSum() THEN storeSum() + '\n\n' ELSE '') + toChar(sum(s),'99999999.99');
            storeQuantityTotal()    <- storeQuantityTotal() (+) quantity(s);
            storeSumTotal()         <- storeSumTotal() (+) sum(s);
        }
        value(TemplateEntry d) <- OVERRIDE storeQuantity(),'' WHERE key(d) = 'STORE_QUANTITY' AND template(d) == t;
        value(TemplateEntry d) <- OVERRIDE storeQuantity(),'' WHERE key(d) = 'STORE_TABLE_QUANTITY' AND template(d) == t;
        value(TemplateEntry d) <- OVERRIDE storePrice(),'' WHERE key(d) = 'STORE_PRICE' AND template(d) == t;
        value(TemplateEntry d) <- OVERRIDE storeSum(),'' WHERE key(d) = 'STORE_SUM' AND template(d) == t;
        value(TemplateEntry d) <- toChar((OVERRIDE storeQuantityTotal(),0),'99999999.99') WHERE key(d) = 'STORE_TOTAL_QUANTITY' AND template(d) == t;
        value(TemplateEntry d) <- toChar((OVERRIDE storeSumTotal(),0),'99999999.99') WHERE key(d) = 'STORE_TOTAL_SUM' AND template(d) == t;
        
        // СПРАВОЧНО в валюте
        debt() <- GROUP MAX Debt d IF act(d) = ag;
        IF inCurrency(debt()) THEN {
            LOCAL rate = NUMERIC[10,4]();
            value(TemplateEntry d) <- replace(trim(toChar((OVERRIDE sumCurrency(debt()),0),'99999999.99')),'.',',') WHERE key(d) = 'SUM_DOCUMENT_CURRENCY' AND template(d) == t;
            value(TemplateEntry d) <- usdWords((OVERRIDE sumCurrency(debt()),0)) WHERE key(d) = 'SUM_USD_WORDS' AND template (d) == t;
            value(TemplateEntry d) <- toDateDDMMYY(firstDayOfMonth(date(ag))) WHERE key(d) = 'RATE_DATE' AND template(d) == t;
            rate() <- round4(1 / OVERRIDE rateOn(defaultTypeExchange(),defaultCurrency(),firstDayOfMonth(date(ag))),1);
            value(TemplateEntry d) <- replace(trim(toChar(rate(),'999.9999')),'.',',') WHERE key(d) = 'RATE_CURRENCY' AND template(d) == t;
        }
        
        // Процент от лицензии или что то типа такого со словом процент
        procLisence() <- 100 * price(GROUP MAX ContractSubject s IF chkData(s, ag) AND isISubstring(nameType(s),'Процент')) ;
        IF NOT procLisence() THEN procLisence() <- 100 * price(GROUP MAX AgreementSubject s IF chkData(s, ag) AND isISubstring(nameType(s),'Процент'));
        value(TemplateEntry d) <- num2str(procLisence()) + '%' WHERE key(d) = 'PROC_LISENCE' AND template(d) == t;
    }    
}

EXTEND FORM documentTemplates
    PROPERTIES copy(tc) DRAW tc TOOLBAR FIRST 
;