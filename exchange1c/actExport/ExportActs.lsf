MODULE ExportActs;

REQUIRE DocDashboardNew, StructureXml, SetExport, ImportGuide, Utils;

chkAct 'Выбранные акты' (Document o) = IF in(o) AND (nameType(o) = 'АСР' OR nameType(o) = 'АКЛ' OR nameType(o) = 'АКТ') THEN TRUE ELSE NULL;

CLASS MenuExport1C 'Меню экспорта в 1С' { p1 '1. Экспорт в 1С (рублевый)', s1 '2. Настройка экспорта в 1С' }

FORM dlgExport 'Меню экспорта в 1С' 
    OBJECTS o = MenuExport1C 
    PROPERTIES(o) READONLY staticCaption 
    ORDERS staticCaption(o)
;

DESIGN dlgExport {
    caption = NULL; 
    PROPERTY (staticCaption(o)) {caption='Выбор действий';}
}

onExportActs 'Экспорт 1С' () {
    IF NOT (GROUP MAX(Document o IS Document) IF chkAct(o)) THEN {
        MESSAGE 'Нет отмеченных актов для экспортв\nОперация прервана'; RETURN;
    }
    onImportGuid(); 
    IF NOT ImportGuide.guidOk() THEN RETURN;
    FOR (Document d IS Document) AND chkAct(d) DO {
        generateUUID();
        guid(d) <- generatedUUID(); // локальное свойство, чтобы не делать apply
    }
    predefinedData() <- ''; // чтобы сформировал тег PredefinedData, а то будет ошибка структуры
    EXPORT v_1CV8DtUD XML CHARSET 'UTF-8-BOM' TO xmlFile;
    in(Document d) <- NULL;
    open(xmlFile());
    CANCEL; // отменяем сохранение данных в TempClient и TempContract - временные данные из 1С, иначе будет светиться Сохранить / Отменить на форме
}

onExport 'Экспорт 1С' () {
    LOCAL item = STRING ();
    DIALOG dlgExport OBJECTS o INPUT DO {
        item() <-  getWord(staticName(o),'.',2);
        CASE
            WHEN item() = 'p1' THEN onExportActs();
            WHEN item() = 's1' THEN onEditSet1C();
    }
}


EXTEND FORM docDashboard PROPERTIES onExport();

DESIGN docDashboard { 
    filter {
        MOVE PROPERTY (onExport());
    }
}
