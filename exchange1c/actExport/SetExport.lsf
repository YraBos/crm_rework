MODULE SetExport;

REQUIRE Utils;

// Настройки экспорта в 1С
CLASS set1C 'Настройки для экспорта 1С' {
    p1 'v8:Организация', p2 'v8:СчетРеализации', p3 'v8:Автор', 
    p4 'v8:Валюта=BYN', p5 'v8:ВидДокументаЭСЧФ', p6 'v8:ХозОперация', p7 'v8:СтавкаНДС',
    p8 'v8:Posted',
    a10 'v8:Субконто1=АСР', a11 'v8:Субконто2=АСР',
    a20 'v8:Субконто1=АКЛ', a21 'v8:Субконто2=АКЛ',
    a30 'v8:Субконто1=АКТ', a31 'v8:Субконто2=АКТ'
}

value 'Значение' = DATA STRING (set1C);

FORM editSet1C 'Настройки экспорта'
    OBJECTS o = set1C PANEL
    PROPERTIES (o) READONLY staticCaption PROPERTIES (o) value EDIT set1C OBJECT o;

DESIGN editSet1C {
    OBJECTS {
        NEW cnt {
            fill = 1;
            horizontal = FALSE;
            MOVE PROPERTY (staticCaption(o)) {caption = 'Параметр'; background= RGB(204, 204, 0);}
            MOVE PROPERTY (value(o));
        }
    }
}

FORM viewSet1C 'Настройки экспорта'
    OBJECTS o = set1C
    PROPERTIES (o) READONLY staticCaption, value
    PROPERTIES (o) NEWSESSION EDIT ORDERS staticCaption(o);

DESIGN viewSet1C { PROPERTY (staticCaption(o)) {caption = 'Параметр';}}

onEditSet1C 'Настройка экспорта 1C' {SHOW viewSet1C DOCKED;}

emptyRef 'Пустая ссылка' = '00000000-0000-0000-0000-000000000000';

// сойства-константы из настроек
constBlankRef = emptyRef();
constOrganization 'Люксофт' = OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:Организация'), emptyRef();
constHozOper 'Хоз. операция' = OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:ХозОперация'), emptyRef();
constCurrency 'Валюта' = OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:Валюта=BYN'), emptyRef();
constAccountSale 'Счет реализации' =  OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:СчетРеализации'), emptyRef();
constTypeDoc 'ВидДокументаЭСЧФ'  =  OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:ВидДокументаЭСЧФ'), emptyRef();
constAuthor 'Автор документа'  =  OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:Автор'), emptyRef();
constVat 'Ставка НДС' = OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:СтавкаНДС'), emptyRef();
constPosted 'Проведено' = IF value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:Posted') THEN TTRUE ELSE TFALSE; 

//constSubConto1 'Субконто1'  =  OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:Субконто1=АСР'), emptyRef();
//constSubConto2 'Субконто2'  =  OVERRIDE value(GROUP MAX (set1C s) IF staticCaption(s) = 'v8:Субконто2=АСР'), emptyRef();


// =================== АВТОМАТИЧЕСКОЕ ЗАПОЛНЕНИЕ СВОЙСТВ ===================
GROUP data EXTID 'V8Exch:Data';
GROUP uslugi EXTID 'v8:DocumentObject.ОказаниеУслуг';

ref = DATA LOCAL STRING(INTEGER);

FORM importSet FORMEXTID 'V8Exch=http://www.1c.ru/V8/1CV8DtUD/:_1CV8DtUD'
    PROPERTIES ATTR ='http://v8.1c.ru/data' EXTID 'xmlns:core', ='http://v8.1c.ru/8.1/data/enterprise/current-config' EXTID 'xmlns:v8', ='http://www.w3.org/2001/XMLSchema' EXTID 'xmlns:xs', ='http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:xsi'
    OBJECTS uslugi = INTEGER EXTID 'v8:DocumentObject.ОказаниеУслуг' IN data
    PROPERTIES(uslugi) ref EXTID 'v8:Ref'
    FILTERS imported(uslugi)
;

META ImportRef(name,groupExtid)
    GROUP group##name EXTID groupExtid;
    prop##name = DATA LOCAL STRING(INTEGER); 
    EXTEND FORM importSet PROPERTIES(uslugi) prop##name IN group##name EXTID 'value';
END

@ImportRef(Organization,'v8:Организация');
@ImportRef(HozOper,'v8:ХозОперация');
@ImportRef(Currency,'v8:Валюта');
@ImportRef(AccountSale,'v8:СчетРеализации');
@ImportRef(TypeDoc,'v8:ВидДокументаЭСЧФ');
@ImportRef(Author,'v8:Автор');
@ImportRef(Vat,'v8:СтавкаНДС');

// Примечание по субконто:
// Виды деятельности (субконто 1) всегда разные и зависят от типа акта (АСР, АКЛ, АКТ), вносятся вручную в настройках
// Виды вариантов реализации (субконто 2) - общий для всех видов деятельности
@ImportRef(Subconto2,'v8:Субконто2');

onReplace 'Заполнение значения' (STRING caption,value) {
    LOCAL o = set1C ();
    o() <- GROUP MAX (set1C s) IF staticCaption(s) = caption;
    IF o() THEN value(o()) <- value;    
}

onAutoFill 'Заполнить автоматически' () {
    ASK 'Операция заполнит параметры типа < v8:* > автоматически\n' +
        'на основания файла < demoakt.xml > - импорт из 1С\n' +
        'см. < Документы.ОказанныеУслуги >. Продолжить ?' DO {
        INPUT xmlFile = NAMEDFILE DO {
            IF NOT (upper(name(xmlFile)) = 'DEMOAKT' AND upper(extension(xmlFile)) = 'XML') THEN {
                MESSAGE 'Не ожидаемое имя файла < DEMOAKT.XML >\nОперация прервана';
                RETURN;
            }
            IMPORT importSet XML FROM xmlFile;
            FOR ref(INTEGER i) ORDER i DO {
                IF i > 0 THEN BREAK; // нам не надо сканировать весь файл
                onReplace('v8:Организация', propOrganization(i));
                onReplace('v8:ХозОперация', propHozOper(i));
                onReplace('v8:Валюта=BYN', propCurrency(i));
                onReplace('v8:СчетРеализации', propAccountSale(i));
                onReplace('v8:ВидДокументаЭСЧФ', propTypeDoc(i));
                onReplace('v8:Автор', propAuthor(i));
                onReplace('v8:СтавкаНДС', propVat(i));
                onReplace('v8:Субконто1=АСР',emptyRef()); // надо заполнить вручную
                onReplace('v8:Субконто2=АСР',propSubconto2(i));
                onReplace('v8:Субконто1=АКЛ',emptyRef()); // надо заполнить вручную
                onReplace('v8:Субконто2=АКЛ',propSubconto2(i));
                onReplace('v8:Субконто1=АКТ',emptyRef()); // надо заполнить вручную
                onReplace('v8:Субконто2=АКТ',propSubconto2(i));
            }
            APPLY;
        }
    }
}

EXTEND FORM viewSet1C PROPERTIES onAutoFill() TOOLBAR;

