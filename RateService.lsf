MODULE RateService;

REQUIRE  CustomerNew, Act, Currency, Utils, Time, ContractSubject;

CLASS CustomerExtensions 'Расширение клиента';
TABLE customerExtensions(CustomerExtensions);

customer    'Клиент'        = DATA Customer         (CustomerExtensions) AUTOSET;
date        'Дата действия' = DATA DATE             (CustomerExtensions) NONULL;
rate        'Коэффициент'   = DATA NUMERIC[14,4]    (CustomerExtensions) NONULL;

//rateCureency 'Коэф. USD' (Act a) = 
//    NUMERIC[10,4](round4(1 / OVERRIDE rateOn(defaultTypeExchange(),currency(contract(a)),date(a)),4)); 

rateCureency 'Коэф. USD' (Act a) = 
    NUMERIC[10,4](IF shortNameCurrency(contract(a)) = 'BYN' THEN 
        round4(1 / OVERRIDE rateOn(defaultTypeExchange(),currency(contract(a)),date(a)),4) ELSE 1); 

dateStart 'Период с'      = DATA LOCAL DATE ();
dateEnd   'по'            = DATA LOCAL DATE ();
extReport 'Расшир. отчет' = DATA LOCAL BOOLEAN ();

// правило, связанное с наличием строк расширения для клиента 
permRule(Act a) = GROUP MAX CustomerExtensions e IF date(e) <= date(a) AND customer(e)=customer(a);
// расчет % за обслуживания с учетом правил permRule 
rateService 'Проц. обслуживания' (Act a) = OVERRIDE rate(permRule(a)), 0.0125;

// типы актов, которые могут участвовать в формировании отчета
chkNameType (Act a) = 
    IF isWordInCSV('SP', clientType(customer(a))) THEN isWordInCSV(nameType(a),'АКТ,АКЛ') ELSE
    IF isWordInCSV('SL', clientType(customer(a))) THEN nameType(a) = 'АКЛ'; 

sumRate 'Сумма за обсл.' (Act a) = NUMERIC[14,2](round2( sum(a) * rateService(a) ));
sumRateCurrency 'Сумма за обсл. USD' (Act a) = NUMERIC[14,2](round2( sumRate(a) / rateCureency(a) ));
sumAct 'Сумма акта' (Act a) = NUMERIC[14,2](round2(sum(a)));
sumActCurrency 'Сумма акта, USD' (Act a) = NUMERIC[14,2](round2( sumAct(a) / rateCureency(a) ));

// основное условие включения данных в отчет 
exprAct '' (Act a) = 
    IF chkNameType(a) AND   
    ( nameType(contract(a)) = 'Лицензия' OR nameType(contract(a)) = 'Лицензия ТСД' OR nameType(contract(a)) = 'Сопровождение' ) 
    THEN TRUE;

//rateCureency2 'Коэф. USD' (ContractSubject s) = 
//    NUMERIC[10,4](round4(1 / OVERRIDE rateOn(defaultTypeExchange(),currency(contract(s)),date(s)),4)); 

rateCureency2 'Коэф. USD' (ContractSubject s) = 
    NUMERIC[10,4](IF shortNameCurrency(contract(s)) = 'BYN' THEN 
        round4(1 / OVERRIDE rateOn(defaultTypeExchange(),currency(contract(s)),date(s)),4) ELSE 1); 
    
// количество - это сумма (quantity), сумма - сумма (sum) % обслуживания, price - это % обслуживания (не нужен) 
// в рублях сальдо на начало
s11 '' (LegalEntity l) = 
    GROUP MAX (quantity(ContractSubject s) * IF inCurrency(contract(s)) THEN rateCureency2(s) ELSE 1) 
    IF (nameType(contract(s)) = 'Сопровождение' OR nameType(contract(s)) = 'Лицензия' OR nameType(contract(s)) = 'Лицензия ТСД') AND 
    date(s) <= dateStart() BY legalEntity(contract(s)); 
s12 '' (LegalEntity l) = 
    GROUP MAX (sum(ContractSubject s)  * IF inCurrency(contract(s)) THEN rateCureency2(s) ELSE 1)
    IF (nameType(contract(s)) = 'Сопровождение' OR nameType(contract(s)) = 'Лицензия' OR nameType(contract(s)) = 'Лицензия ТСД') AND 
    date(s) <= dateStart() BY legalEntity(contract(s)); 
// в USD сальдо на начало
s21 '' (LegalEntity l) = 
    GROUP MAX (quantity(ContractSubject s) / IF inCurrency(contract(s)) THEN 1 ELSE rateCureency2(s)) 
    IF (nameType(contract(s)) = 'Сопровождение' OR nameType(contract(s)) = 'Лицензия' OR nameType(contract(s)) = 'Лицензия ТСД') AND 
    date(s) <= dateStart() BY legalEntity(contract(s)); 
s22 '' (LegalEntity l) = 
    GROUP MAX (sum(ContractSubject s)  / IF inCurrency(contract(s)) THEN 1 ELSE rateCureency2(s))
    IF (nameType(contract(s)) = 'Сопровождение' OR nameType(contract(s)) = 'Лицензия' OR nameType(contract(s)) = 'Лицензия ТСД') AND 
    date(s) <= dateStart() BY legalEntity(contract(s)); 

sb      'BYN'       (LegalEntity l) = NUMERIC[14,2](round2( IF s11(l) THEN s11(l) ELSE 0.00 ));
sbr     'BYN(%)'    (LegalEntity l) = NUMERIC[14,2](round2( IF s12(l) THEN s12(l) ELSE 0.00 ));
sc      'USD'       (LegalEntity l) = NUMERIC[14,2](round2( IF s21(l) THEN s21(l) ELSE 0.00 ));
scr     'USD(%)'    (LegalEntity l) = NUMERIC[14,2](round2( IF s22(l) THEN s22(l) ELSE 0.00));

// расчет итогов на конец, через JR

// заголовки для отчета 
title1 '' = DATA LOCAL TEXT ();  title2 '' = DATA LOCAL TEXT (); title3 '' = DATA LOCAL TEXT ();

FORM repProcService 'Отчет процент за обслуживание'
    OBJECTS c = Customer PROPERTIES title1(), title2(), title3() PANEL
    OBJECTS l = LegalEntity PROPERTIES(l) name, sb, sbr, sc, scr PANEL
    OBJECTS d = Contract PROPERTIES(d) numberContract, dateContract, name  PANEL
    OBJECTS a = Act PROPERTIES(a) number, date, name, nameType, sumAct, rateService, sumRate, rateCureency, sumActCurrency, sumRateCurrency
    FILTERS customer(l) = c, legalEntity(d) = l, contract(a) = d
    FILTERS IF extReport() THEN date(a) <= dateEnd() ELSE date(a) >= dateStart() AND date(a) <= dateEnd()  
    FILTERS exprAct(a)
    ORDERS date(a)
;

dateToStr = FORMULA STRING[8] 'to_char(($1),\'DD-MM-YYYY\')';
onRepProcent 'Отчет %' (Customer c) {
    LOCAL frep = FILE();
    LOCAL fname = STRING[100] ();
    fname() <- trim(name(c)) + '_' + dateToStr(dateStart()) + '_' + dateToStr(dateEnd());
    title1() <- 'Расчет процентов за обслуживание';
    title2() <- 'за период с ' + toDateDDMMYY(dateStart()) + ' по '  + toDateDDMMYY(dateEnd()) + 'г.';
    title3() <- 'клиент: ' + trim(name(c));
    PRINT repProcService OBJECTS c = c XLS TO frep; 
    open(frep(), fname() + '.xls');                         
//    PRINT repProcService OBJECTS c = c; // отладка 
}; 


FORM editCustomerExtensions
    OBJECTS ce = CustomerExtensions PANEL
    PROPERTIES(ce) date, rate
    EDIT CustomerExtensions OBJECT ce
;


EXTEND FORM customers
    PROPERTIES(o) onRepProcent
    PROPERTIES() dateStart, dateEnd, extReport 
    EVENTS ON INIT {
        dateStart() <- currentDate();
        dateEnd() <- currentDate();
//        dateStart() <- 2020_11_01;            // отладка
//        dateEnd() <- 2020_11_30;
    }
   OBJECTS ce = CustomerExtensions
   PROPERTIES(ce) READONLY date, rate
   PROPERTIES(ce) NEWSESSION NEW, DELETE, EDIT 
   ORDERS date(ce)
   FILTERS customer(ce) = o 
//   FILTERS name(o) = 'Санта' OR name(o) = 'Узбекистан'             // Отладка, Санта  
;

DESIGN customers {
    BOX(ce) {
        flex = 0.1;
    }
    cnt10 {
        NEW cnt3 {
            marginLeft = 10;
            caption = '% обслуживания'; 
            type = CONTAINERH ;
            MOVE PROPERTY (dateStart());
            MOVE PROPERTY (dateEnd());
            MOVE PROPERTY (extReport());
            MOVE PROPERTY (onRepProcent(o));
        }
    }
}