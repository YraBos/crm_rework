MODULE InfoFusion;

REQUIRE InfoFusionTopic, Employee, Dashboard, Issue, Customer, Utils;


CLASS InfoFusion 'База знаний lsFusion';
TABLE infofusion (InfoFusion);

date        'Дата'                  = DATA DATE             (InfoFusion) NONULL;
lession     'Урок'                  = DATA INTEGER          (InfoFusion) CHARWIDTH 5;
employee    'ID сотрудника'         = DATA Employee         (InfoFusion) NONULL;
topic       'ID раздела'            = DATA InfoFusionTopic  (InfoFusion) NONULL;
theme       'Тема занятия'          = DATA STRING[100]      (InfoFusion) NONULL;
customer    'ID клиента'            = DATA Customer         (InfoFusion);     
description 'Описание занятия'      = DATA TEXT             (InfoFusion);

guru 'Докладчик' (InfoFusion o)   = name(employee(o));
nameTopic 'Раздел' (InfoFusion o) = name(topic(o));
nameCustomer 'Клиент' (InfoFusion o) = name(customer(o));

CLASS InfoFusionResource 'Ресурсы';
TABLE inforusionresource (InfoFusionResource);

CLASS InfoFusionTypeResource 'Тип ресурса' {
    p101 'FILE', p102 'WEB', p103 'RM', p104 'LS',p900 'NONE'
}

infoFusion  'ID шапки'              = DATA InfoFusion       (InfoFusionResource) AUTOSET;
date        'Дата'                  = DATA DATE             (InfoFusionResource) NONULL;
type        'ID типа ресурса'       = DATA InfoFusionTypeResource (InfoFusionResource) NONULL;
link        'Ссылка на ресурс'      = DATA STRING[200]      (InfoFusionResource) NONULL;
description 'Описание'              = DATA STRING[200]      (InfoFusionResource) NONULL;

nameType 'Тип ресурса' (InfoFusionResource r) = staticCaption(type(r)) CHARWIDTH 8;

onRedMine 'Redmine' (InfoFusionResource r) {
    DIALOG dialogIssues OBJECTS o INPUT DO {
        type(r) <- GROUP MAX (InfoFusionTypeResource o1) IF isSubstring(staticCaption(o1),'RM');
        link(r) <- STRING[200] (number(o));
        description(r) <- STRING[200] (name(o));
    }
}

FORM editInfoFusionResource 'Редактирование'
    OBJECTS r = InfoFusionResource PANEL
    PROPERTIES(r) date, nameType, link, description
    PROPERTIES(r) onRedMine  
    EDIT InfoFusionResource OBJECT r
    EVENTS ON INIT {
        IF NOT date(r) THEN date(r) <- currentDate();
    }
;

DESIGN editInfoFusionResource {
    OBJECTS {
        NEW cnt1 {
            fill = 1;
            MOVE PROPERTY (date(r)) {caption = 'Дата создания ресурса';};
            NEW cnt2 {
                horizontal = TRUE;
                MOVE PROPERTY (nameType(r));
                MOVE PROPERTY (onRedMine(r));
            }
            MOVE PROPERTY (link(r));
            MOVE PROPERTY (description(r));  
        }
    }
}

onShow 'Показать' (InfoFusionResource r) {
    LOCAL cname = STRING[4] ();
    cname() <- trim(nameType(r));
    IF cname() = 'FILE' THEN open(link(r));
    ELSE IF cname() = 'WEB' THEN open(LINK(link(r))); 
    ELSE IF cname() = 'RM' THEN open(LINK('http://support.luxsoft.by/issues/' + link(r))); 
    ELSE IF cname() = 'LS' THEN {
        LOCAL importFile = FILE ();
        LOCAL sconnect, cmsg = STRING();
        cmsg() <- 'Прием и отображение запрашиваемого ресурса\nможет занять несколько минут или более\n';
        cmsg() <- cmsg() + 'Ресурс доступен по адресу:\n//SERVER2/WorkUser/employeeteaching/' + link(r) + '\n';
        cmsg() <- cmsg() + 'Вы согласны продолжить?';
        ASK cmsg() DO {
            sconnect() <- 'ftp://piu:963741@office.luxsoft.by/' + link(r);
            READ sconnect() TO importFile;
            open(importFile());
        }
    } 
    ELSE MESSAGE 'Не обрабатываемый тип';
}

otop '' = DATA InfoFusionTopic ();          // указатель перехода на раздел, после редактирования темы занятия. См. editInfoFusion, edit +
descriptionText = DATA LOCAL TEXT ();       // обманка для description. Если 2-й грид не имеет записей, то табло пропадает
                                            // обманка исправляет "прыгание" интерфейсов 

fltDescription 'Фильтр темы' = DATA LOCAL STRING[20] ();                                          
onClsFlt 'X' () {
    fltDescription() <- NULL;
}
                                            
FORM viewInfoFusion 'Проводимые занятия'
    TREE ot t = InfoFusionTopic PARENT parent(t)
    PROPERTIES(t) READONLY name
    PROPERTIES(t) NEWSESSION DELETE, NEW, EDIT 
    ORDERS name(t)

    OBJECTS o = InfoFusion
    PROPERTIES( ) READONLY PANEL descriptionText 
    PROPERTIES(o) READONLY date, lession, guru, theme, nameCustomer
    PROPERTIES(o) NEWSESSION DELETE, NEW, EDIT
    FILTERGROUP  tflt
        FILTER ' Привязка к разделам' level(topic(o), t) DEFAULT

    OBJECTS r = InfoFusionResource
    PROPERTIES(r) READONLY date, nameType, description, link
    PROPERTIES(r) NEWSESSION DELETE, NEW, EDIT
    PROPERTIES(r) TOOLBAR onShow 
    FILTERS infoFusion(r) = o
    ORDERS date(o)
    
    PROPERTIES fltDescription(), onClsFlt()
    FILTERS IF fltDescription() THEN isISubstring(description(o),fltDescription()) ELSE TRUE  
    
    EVENTS ON CHANGE o {
        descriptionText() <- OVERRIDE description(o), '';       
    }
    EVENTS ON INIT {
        LOCAL top = InfoFusionTopic ();
        FOR (InfoFusionTopic ob IS InfoFusionTopic) ORDER level(ob,parent(ob)), name(ob) DO {
            IF NOT top() THEN top() <- ob;
            {EXPAND UP viewInfoFusion.t OBJECTS t = ob;}
        }
    }
; 




FORM editInfoFusion 'Редактирование'
    OBJECTS o = InfoFusion PANEL 
    PROPERTIES(o) date, lession, guru
    PROPERTIES(o) nameTopic ON CHANGE {DIALOG listInfoFusionTopic OBJECTS t INPUT DO topic(o) <-t;}
    PROPERTIES(o) theme, nameCustomer, description
    EDIT InfoFusion OBJECT o
    EVENTS ON INIT {
        IF NOT date(o) THEN date(o) <- currentDate();
        IF NOT employee(o) THEN employee(o) <- currentUser();
        otop() <- NULL;
    }
    EVENTS ON OK {
        otop() <- GROUP MAX (InfoFusionTopic t) IF name(t) = nameTopic(o);
    }
;

edit (InfoFusion i) + {
    SHOW editInfoFusion OBJECTS o=i WAIT DOCKED;
    IF otop() THEN SEEK viewInfoFusion.t = otop(); 
}

DESIGN editInfoFusion {
    OBJECTS {
        NEW cntMain {
            fill = 1;
            NEW cnt1 {
                MOVE PROPERTY (date(o));
                MOVE PROPERTY (lession(o));
                MOVE PROPERTY (guru(o));
                MOVE PROPERTY (nameTopic(o));
                MOVE PROPERTY (theme(o));
                MOVE PROPERTY (nameCustomer(o));
            }
            NEW cnt2 {
                horizontal = TRUE;
                fill = 1;
                caption = 'Описание темы';
                MOVE PROPERTY (description(o)) {caption = '';} 
            }
        }
    }
}

DESIGN viewInfoFusion {
    PANEL {
        NEW tablo {
            horizontal = TRUE;
            fill = 1;
            caption = 'Описание темы';
            MOVE PROPERTY (descriptionText()) {caption = '';}
        }
    }
    OBJECTS {
        NEW cntV {
            fill = 1;
            NEW cnt1 {
                fill = 5;
                horizontal = TRUE;
                MOVE BOX (TREE ot) {caption = 'Список разделов';};
                MOVE BOX(o) {fill = 6;}
            }
            NEW cnt2 {
                horizontal = TRUE;
                fill = 1;
                MOVE BOX(r);
            }
        } 
    }
    TOOLBARRIGHT (o) {
        NEW cnt3 FIRST {
            horizontal = TRUE ;
            MOVE PROPERTY (fltDescription());
            MOVE PROPERTY (onClsFlt()) {width = 40;};
            MOVE FILTERGROUP (tflt);
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW viewInfoFusion;
    }
} 
  