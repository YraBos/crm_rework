MODULE EquipmentReports;

REQUIRE EquipmentDataExt, TempClassReports, ReviseTSDAll, EquipmentDashboard, CustomerNew, EquipmentDataImport;

CLASS MenuReports 'Отчеты' {
    p1 ' 1. Выданные s/n ТСД и анализ подключений',
    p2 ' 2. История замен s/n',
    p3 ' 3. Отчет по сверке работающих ТСД (простой)',
    p4 ' 4. Сверка лицензий ТСД',
    p5 ' 5. Отчет: Список выданных ключей ТСД'
}

FORM viewMenuReports 'Отчеты'
    OBJECTS ob = MenuReports
    PROPERTIES(ob) READONLY staticCaption
    ORDERS staticCaption(ob) 
;

DESIGN viewMenuReports { 
    PROPERTY (staticCaption(ob)) {
        caption = 'Название отчета';
    }
}

number 'Номер'          = DATA STRING[250]  (TempClassReports);
dLast 'Дата пос. исп.'  = DATA DATE         (TempClassReports);
dCreate 'Дата выдачи'   = DATA DATE         (TempClassReports);
found 'Найден'          = DATA STRING[20]   (TempClassReports);

FORM repTempEQUReport 'Сверка торговых объектов'
  PROPERTIES() title1, title2, title3, title4 PANEL  
  OBJECTS o1 = TempClassReports
  PROPERTIES(o1) pi, number, dLast, dCreate, found
  ORDERS pi(o1) 
  REPORT 'EquipmentDashboardNew_repTempEQUReport.jrxml' 
;

// R14583, сверка серийных номеров по договору (упрощенный отчет)
onReviseTSD 'Сверка раб. ТСД (упр.)' (Equipment e) {
    newTempClassReports();
    LOCAL ot = Attachment();
    LOCAL colC = STRING[250] (INTEGER);
    LOCAL colE = DATE (INTEGER);
    LOCAL ctxt = STRING[250] ();
    LOCAL cnt, cnt1, cnt2 = INTEGER(); cnt() <- 0; cnt1() <- 0; cnt2() <- 0; 
    ot() <- GROUP MAX Attachment a IF customer(a) = customer(e) AND name(type(a)) = 'Список работающих ТСД';
    IF NOT ot() THEN { 
        MESSAGE 'Не найден документ:\nСписок работающих ТСД'; 
        RETURN; 
    }
    IMPORT XLS NOHEADER FROM file(ot()) AS FILE TO colC = C, colE = E;
    FOR imported(INTEGER i) AND i > 1 AND colC(i) AND oserial(filterCustomer(), colC(i)) ORDER i DO NEW ob = TempClassReports {
        cnt()  <- cnt() + 1;
        number(ob) <- colC(i); 
        dLast(ob) <- colE(i);
        pi(ob) <- 2;
        cnt2() <- cnt2() + 1;
        found(ob) <- 'ок';
        dCreate(ob) <- createdDate(GROUP MAX (Equipment e1) IF e1 = equipment(oserial(filterCustomer(), colC(i))));
    }    
    FOR imported(INTEGER i) AND i > 1 AND colC(i) AND NOT oserial(filterCustomer(), colC(i)) ORDER i DO NEW ob = TempClassReports {
        cnt()  <- cnt() + 1;
        number(ob) <- colC(i); 
        dLast(ob) <- colE(i);
        pi(ob) <- 1;
        cnt1() <- cnt1() + 1;
        found(ob) <- 'не найден';
    }    
    title1() <- 'Отчет по сверке работающих ТСД';
    title2() <- 'по клиенту: ' + name(customer(e));
    title3() <- 'Обработано всего: ' + trim(toChar(cnt(),'999999')) + ', ' + 
                'найдено в БД: ' + trim(toChar(cnt2(),'999999')) + ', не найдено: ' + trim(toChar(cnt1(),'999999')); 
    title4() <- 'Отчет формируется относительно содержимого документа "Список работающих ТСД". ' + 
                'Серийные номера (sn) из документа последовательно сверяются с sn в базе данных по клиенту.\n' +
                'Если sn найден, то заполняется дата выдачи и значение колонки "выдан" - ok, иначе пишется: не найден';
    PRINT repTempEQUReport XLS; // XLS
}

onReviseS2 'Сверка лицензий ТСД' (Equipment e) {
    newTempClassReports();    
    title() <- 'Отчет по сверке выданных лицензий ТСД';
    date() <- currentDate();
    FOR (Customer c IS Customer) DO inReport(c) <- NULL;
    FOR (Customer c IS Customer) AND c=customer(e) DO inReport(c) <- TRUE;
    onRevise(3,'Список лицензий ТСД','Список выданных ключей ТСД','S2',NULL);
    PRINT repReviseTSD XLS; // XLS;
}

FORM printHistory 'История замен'
    PROPERTIES equTitle1(), equTitle2(), equTitle3()
    OBJECTS e = Equipment
    PROPERTIES (e) createdDate, createdTime, nameCreatedUser, nameContract, nameLegalEntityContract
    FILTERS customer(e) = filterCustomer()  
    OBJECTS es = EquipmentSerial
    PROPERTIES (es) number, dateChange, numberChange, note
    FILTERS equipment(es) = e, numberChange(es)
    OBJECTS esc = EquipmentSerialChange
    PROPERTIES (esc) numberNew, date, numberChange, note
    FILTERS equipmentSerial(esc) = es 
    ORDERS createdDate(e), date(esc)
    REPORT 'Equipment_printHistory.jrxml'
;

cntChange (Equipment equ) = GROUP SUM 1 IF numberChange(EquipmentSerial es) AND equipment(es) = equ;

onPrintHistory 'История замен' (Equipment e) {
    LOCAL ncnt = INTEGER ();
    FOR (Equipment o IS Equipment) AND customer(o) = filterCustomer() DO ncnt() <- ncnt() (+) cntChange(o); 
    equTitle1() <- name(filterCustomer());
    equTitle2() <- toDateDDMMYY(currentDate());
    equTitle3() <- toChar(ncnt(),'9999');
    PRINT printHistory XLS;
}

onMenuReports 'Меню: Отчеты' (Equipment e,SubjectType t) {
    IF NOT filterCustomer() THEN { MESSAGE 'Определите клиента'; RETURN ; }
    LOCAL ov  = MenuReports();
    LOCAL ps  = STRING[3] ();
    DIALOG viewMenuReports OBJECTS ob INPUT DO { ov() <- ob; }
    IF NOT ov() THEN RETURN;
    ps() <- substr(staticCaption(ov()),1,3);
    IF ps() = ' 1.' THEN {
        ReviseTSDAll.onReviseTSDAll(e);
    } ELSE IF ps() = ' 2.' THEN {
        onPrintHistory(e);
    } ELSE IF ps() = ' 3.' THEN {
        onReviseTSD(e);
    } ELSE IF ps() = ' 4.' THEN {
        onReviseS2(e);
    } ELSE IF ps() = ' 5.' THEN {
        PRINT repOutSerials OBJECTS c = customer(e),t = t XLS; 
    } ELSE {
        MESSAGE 'Неизвестный отчет'; RETURN;
    }
}    
