MODULE EquipmentDasboardChange;

REQUIRE EquipmentDashboardNew, Time;

NAMESPACE Equipment;

// Сохраняем в EquipmentSerial - данные о последнем изменении
// Сохраняем в EquipmentSerialChange - все изменения
CLASS EquipmentSerialChange 'История замены серийных номеров';
equipmentSerial 'ID' = DATA EquipmentSerial (EquipmentSerialChange) AUTOSET ;
numberChange 'Замененный номер' = DATA ISTRING[250] (EquipmentSerialChange) CHARWIDTH 15;
numberNew 'Новый номер' = DATA ISTRING[250] (EquipmentSerialChange) NONULL  CHARWIDTH 15;
date 'Дата замены' = DATA DATE (EquipmentSerialChange);
note 'Примечание' = DATA STRING[200] (EquipmentSerialChange);


FORM editEquipmentSerialChange 'Замена номера'
    OBJECTS o = EquipmentSerialChange PANEL 
    PROPERTIES (o) READONLY date, numberChange
    PROPERTIES (o) note, numberNew
    EDIT EquipmentSerialChange OBJECT o
    EVENTS ON INIT {
        LOCAL e = EquipmentSerial();
        e() <- GROUP MAX (EquipmentSerial e) IF e = equipmentSerial(o);
        numberChange(o) <- number(e());
        date(o) <- currentDate();
    }
    EVENTS ON OK {
        LOCAL e = EquipmentSerial();
        e() <- GROUP MAX (EquipmentSerial e) IF e = equipmentSerial(o);
        number(e()) <- numberNew(o);   
        numberChange(e()) <- numberChange(o); 
        dateChange(e()) <- date(o);
        note(e()) <- note(o);   
    }
;

DESIGN editEquipmentSerialChange {
    OBJECTS {
        NEW cnt {
            MOVE PROPERTY (date(o)) {background=#E6E6E6;}
            MOVE PROPERTY (numberChange(o)) {caption = 'Заменяемый номер';background=#E6E6E6;}
            MOVE PROPERTY (numberNew(o));
            MOVE PROPERTY (note(o));
        }
    }
}


EXTEND FORM equipmentDashboard
    OBJECTS esc = EquipmentSerialChange LAST
    PROPERTIES (esc) READONLY numberNew, date, numberChange,  note
    PROPERTIES (esc) NEWSESSION NEW SHOWIF TRUE IF number(n) 
    FILTERS equipmentSerial(esc) = n 
;


DESIGN equipmentDashboard {
    serials {
        MOVE BOX(n);
        MOVE BOX(esc) {fill=0.5;};
        MOVE BOX(l);
    }
}

FORM printHistory 'История замен'
    PROPERTIES equTitle1(), equTitle2(), equTitle3()
    OBJECTS e = Equipment
    PROPERTIES (e) createdDate, createdTime, nameCreatedUser, nameContract, nameLegalEntityContract
    FILTERS customer(e) = filterCustomer()  
    OBJECTS es = EquipmentSerial
    PROPERTIES (es) number, dateChange, numberChange, note
    FILTERS equipment(es) = e, numberChange(es)
    OBJECTS esc = EquipmentSerialChange
    PROPERTIES (esc) numberNew, date, numberChange, note
    FILTERS equipmentSerial(esc) = es 
    ORDERS createdDate(e), date(esc)
;

cntChange (Equipment equ) = GROUP SUM 1 IF numberChange(EquipmentSerial es) AND equipment(es) = equ;

onPrintHistory 'История замен' (Equipment e) {
    IF NOT filterCustomer() THEN { MESSAGE 'Определите клиента';  RETURN; }
    LOCAL ncnt = INTEGER ();
    FOR (Equipment o IS Equipment) AND customer(o) = filterCustomer() DO ncnt() <- ncnt() (+) cntChange(o); 
    equTitle1() <- name(filterCustomer());
    equTitle2() <- toDateDDMMYY(currentDate());
    equTitle3() <- toChar(ncnt(),'9999');
    PRINT printHistory XLS;
}

EXTEND FORM equipmentDashboard
    PROPERTIES onPrintHistory(e)
;

DESIGN equipmentDashboard {
    head2 {
        MOVE PROPERTY (onPrintHistory(e));
    }
}
